////////////////////////////////////////////////////////////////////////////////
//{ ПЕРЕМЕННЫЕ МОДУЛЯ
	
	&НаКлиенте
	Перем UserPermissions;
	&НаКлиенте
	Перем Organization Экспорт; 

	&НаКлиенте
	Перем МассивСсылокРНК Экспорт;
	&НаКлиенте
	Перем ПодходящаяСФ;
	
	&НаКлиенте
	Перем ЭДОбъект Экспорт; 
	
	&НаКлиенте
	Перем ПредставлениеСтатусаРоуминг, ПредставлениеСтатусаРоумингДетали;
	
//} ПЕРЕМЕННЫЕ МОДУЛЯ
////////////////////////////////////////////////////////////////////////////////

#Область ПЕРМЕННЫЕ_ПЛАТФОРМЫ

&НаКлиенте
Перем Платформа Экспорт;

&НаСервере
Перем ОбработкаОбъект;

#КонецОбласти

#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ_ПЛАТФОРМЫ

&НаКлиенте
Функция МетодКлиента(ИмяМодуля= "", ИмяМетода, 
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL,
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат  Платформа.МетодКлиента(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаКлиенте
Функция МетодСервераБезКонтекста(ИмяМодуля= "", ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат Платформа.МетодСервераБезКонтекста(ИмяМодуля, ИмяМетода,
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция МетодСервера(Знач ИмяМодуля= "", Знач ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат ОбработкаОбъект().МетодСервера(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция ОбработкаОбъект() Экспорт
	
	Если ОбработкаОбъект = Неопределено Тогда
		
		СтруктураОбработки= ПолучитьИзВременногоХранилища(Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
		
		Если СтруктураОбработки <> Неопределено Тогда
			ОбработкаОбъект= СтруктураОбработки.ОбработкаОбъект;
		КонецЕсли;
		
		Если ОбработкаОбъект = Неопределено Тогда
			
			ОбработкаОбъект= РеквизитФормыВЗначение("Объект");
			
			Попытка
				ПоместитьВоВременноеХранилище(Новый Структура("ОбработкаОбъект", ОбработкаОбъект), Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
			Исключение КонецПопытки;
		
		Иначе
			ОбработкаОбъект.ПараметрыКлиентСервер= Объект.ПараметрыКлиентСервер;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОбработкаОбъект;
	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма(ТекущийВладелецФормы)
	
	Если ТекущийВладелецФормы = Неопределено Тогда
		Возврат Неопределено
	ИначеЕсли Прав(ТекущийВладелецФормы.ИмяФормы, 14) = "Форма_Основная" Тогда
		Возврат ТекущийВладелецФормы;
	Иначе
		Возврат ОсновнаяФорма(ТекущийВладелецФормы.ВладелецФормы);
	КонецЕсли;
	
КонецФункции


&НаСервере
Процедура ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", Объект.ПараметрыКлиентСервер);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатформаПриОткрытии(Отказ)
	
	ОсновнаяФорма= ОсновнаяФорма(ВладелецФормы);
	
	Если ОсновнаяФорма <> Неопределено Тогда
		Платформа= ОсновнаяФорма.Платформа;
	КонецЕсли;
		
	Платформа.ПриОткрытииФормыОбработки(ЭтаФорма, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатформаПриЗакрытии()
	
	Платформа.ПриЗакрытииФормыОбработки(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
//{ СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

	&НаСервере
	Функция ПолучитьСвязаннуюОрганизацию(BoxID)
		
		Возврат МетодСервера(,"BoxID_2_Организация", BoxID);
		
	КонецФункции
	
	&НаКлиенте
	Процедура СообщениеОбОшибкеДиадок(текстОшибки) экспорт 
		
		ПоказатьПредупреждение(, МетодКлиента("Модуль_Клиент","СформироватьТекстОшибкиДиадок", ТекстОшибки), 120, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
		
	конецПроцедуры	
	
	&НаКлиенте
	Процедура ОбновитьЭДОбъект()
		
		ЭДОбъект=	ЭДОбъект.Organization.GetDocumentById(ЭДОбъект.DocumentId);
		ПросмотрФормы();
		ОбновитьСтатусЭД();
		
		ПараметрыОповещения=	Новый Структура;
		ПараметрыОповещения.Вставить("BoxID", ЭДОбъект.OrganizationID);
		ПараметрыОповещения.Вставить("DocumentID", ЭДОбъект.DocumentID);
		
		МетодКлиента(,"ОповеститьФормы", "ИзменениеСтатусаДокументаДиадок", ПараметрыОповещения, ЭтаФорма);
		
	КонецПроцедуры
	
	&НаСервере
	Функция СформироватьТитулПолучателя(СтруктураПодписи, Type, DocumentDate) Экспорт
		Возврат МетодСервера(,"СформироватьТитулПолучателя", СтруктураПодписи, Type, DocumentDate);		
	КонецФункции	
	
	&НаКлиенте
	Процедура ОбновитьДокумент1С()
		
		Документ1С=	ПолучитьDocumentID_2_Документ(ЭДОбъект.DocumentID, ЭДОбъект.OrganizationID);
		
	КонецПроцедуры
	
	&НаСервере
	Функция ПолучитьDocumentID_2_Документ(DocumentID, BoxID)
		
		Возврат МетодСервера(,"DocumentID_2_Документ", DocumentID, BoxID);
		
	КонецФункции
	
	&НаСервере
	Процедура Установить_DocumentID_Для_Документ(Документ, DocumentID, BoxID, ИспользоватьИдентификаторСчета = Ложь, ЭтоУПД_ТипаСЧФДОП)
		
		МетодСервера(,"Установить_DocumentID_Для_Документ", Документ, DocumentID, BoxID, ИспользоватьИдентификаторСчета, ЭтоУПД_ТипаСЧФДОП);
		
	КонецПроцедуры

	&НаСервере
	Процедура ОчиститьCustomDocumentId(Документ, ИспользоватьИдентификаторСчета = Ложь)
		
		МетодСервера(,"ОчиститьCustomDocumentId", Документ, ИспользоватьИдентификаторСчета);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СоздатьДокумент(Режим) Экспорт
		
		Если Режим = "Ввод" Тогда
			
			ПараметрыФормы=	Новый Структура;
			ПараметрыФормы.Вставить("BoxID", 				ЭДОбъект.OrganizationID);
			ПараметрыФормы.Вставить("DocumentID", 			ЭДОбъект.DocumentID);
			ПараметрыФормы.Вставить("Direction", 			ЭДОбъект.Direction);
			ПараметрыФормы.Вставить("Type",					ЭДОбъект.type);
			ПараметрыФормы.Вставить("CounteragentBoxID",	ЭДОбъект.Counteragent.ID);
			
			ПараметрыФормы.Вставить("Контрагент", 			Контрагент);
			ПараметрыФормы.Вставить("Организация", 			Организация);
			
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВводаНакладной", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВводаНакладной");
			
		ИначеЕсли Режим = "Сопоставление" Тогда
			
			ПараметрыФормы=	Новый Структура;
			ПараметрыФормы.Вставить("BoxID", 				ЭДОбъект.OrganizationID);
			ПараметрыФормы.Вставить("DocumentID", 			ЭДОбъект.DocumentID);
			ПараметрыФормы.Вставить("Type", 				ЭДОбъект.Type);
			ПараметрыФормы.Вставить("CounteragentBoxID", 	ЭДОбъект.Counteragent.ID);
			ПараметрыФормы.Вставить("DocumentDate", 		ЭДОбъект.DocumentDate);
			ПараметрыФормы.Вставить("DocumentNumber", 		ЭДОбъект.DocumentNumber);
			ПараметрыФормы.Вставить("Total",				ЭДОбъект.Total);
			ПараметрыФормы.Вставить("Режим", 				Режим);
			ПараметрыФормы.Вставить("Контрагент", 			Контрагент);
			ПараметрыФормы.Вставить("Организация", 			Организация);
			
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыбораДокумента", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВыбораДокумента");
			
		ИначеЕсли Режим = "ВводСВыборомОснований" Тогда
			
			ПараметрыФормы=	Новый Структура;
			ПараметрыФормы.Вставить("BoxID", 				ЭДОбъект.OrganizationID);
			ПараметрыФормы.Вставить("DocumentID", 			ЭДОбъект.DocumentID);
			ПараметрыФормы.Вставить("Type", 				ЭДОбъект.Type);
			ПараметрыФормы.Вставить("CounteragentBoxID",	ЭДОбъект.Counteragent.ID);
			ПараметрыФормы.Вставить("DocumentDate", 		ЭДОбъект.DocumentDate);
			ПараметрыФормы.Вставить("DocumentNumber", 		ЭДОбъект.DocumentNumber);
			ПараметрыФормы.Вставить("Режим", 				Режим);
			ПараметрыФормы.Вставить("Контрагент", 			Контрагент);
			ПараметрыФормы.Вставить("Организация", 			Организация);
			
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыбораДокумента", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВыбораДокументаСОснованием");
			
		КонецЕсли;
		
	КонецПроцедуры
	
//} СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ УПРАВЛЕНИЕ ФОРМОЙ
	
	&НаСервере
	Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
		ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка);
		
		Организация=	МетодСервера(,"BoxID_2_Организация", Параметры.BoxID, Параметры.DepartmentKpp);
		Контрагент=		МетодСервера(,"CounteragentBoxID_2_Контрагент", Параметры.CounteragentBoxID);
		
		Параметры.Свойство("DepartmentId", DepartmentId);
		
		BoxID= Параметры.BoxID;
		Если Параметры.МассивДокументовПакета.Количество() = 1 Тогда
			Документ1С=				Параметры.МассивДокументовПакета[0].Документ1С;
			РасширениеФайлаДиадок=	Параметры.МассивДокументовПакета[0].РасширениеФайлаДиадок;
		КонецЕсли;
		
		ТочкаВызова= Параметры.ТочкаВызова;
						
		ЭтаФорма.Команды.ПерейтиВДиадок.Подсказка = "Перейти в " + МетодСервера(,"ПолучитьСловарь").НаименованиеСистемы;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПерейтиВДиадок(Команда)
		
		Если НЕ ЭдОбъект = Неопределено Тогда
			ПоказатьДокументВДиадоке(ЭдОбъект);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПоказатьДокументВДиадоке(ДокументДиадока)
		
		МетодКлиента("Модуль_Клиент","ПоказатьДокументВДиадоке", ДокументДиадока.OrganizationId, ДокументДиадока.DocumentId);
		
	КонецПроцедуры 
	
	&НаКлиенте
	Функция ПоказатьКнопкуПерейтиВДиадок()
		
		Возврат НЕ ЭДОбъект = Неопределено;

	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуСтруктураПодчиненности()
		
		Возврат НЕ ЭДОбъект = Неопределено;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуОтправитьСвязанныеДокументы()
		
		Возврат НЕ ЭДОбъект = Неопределено И МетодКлиента("Модуль_Клиент","ЭтоНеформализованныйДокументБезМетаданных", ЭДОбъект) = Ложь;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьГруппуКнопокПодписания(UserPermissions)

		Если (НЕ ЭДОбъект = Неопределено И ЭДОбъект.Direction = "Inbound")
			И (НЕ ЛЕВ(ЭДОбъект.Type, 7) = "Invoice" И НЕ ЭДОбъект.Type = "NonformalizedProforma")
			И ((ЭДОбъект.IsTest ИЛИ ЭДОбъект.Organization.AuthenticateType = "Certificate") И UserPermissions.CanSignIncomingDocuments)
			И (ЭДОбъект.Status = "InboundWaitingForRecipientSignature" 
				ИЛИ ЭДОбъект.Status = "InboundInvalidRecipientSignature"
				ИЛИ (ЭДОбъект.Type = "Nonformalized" И ЭДОбъект.Status ="InboundNoRecipientSignatureRequest"))
			И ЭДОбъект.RevocationStatus = "RevocationStatusNone" 
		Тогда
		
			Возврат Истина;
		КонецЕсли;
						
		Возврат Ложь;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуОтменитьСопоставление()

		Если  НЕ ЭДОбъект = Неопределено
			И (ЭДОбъект.Direction = "Inbound" ИЛИ ЭДОбъект.Direction = "Outbound")
			И ЗначениеЗаполнено(Документ1С)
			И МетодКлиента("Модуль_Клиент","ЭтоНеформализованныйДокументБезМетаданных", ЭДОбъект) = Ложь Тогда
			Возврат Истина;
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьГруппуКнопокСогласования(UserPermissions, MyUser)
		
		Если ЭДОбъект <> Неопределено
			И UserPermissions.CanAddResolutions // Пользователь имеет право согласовывать документы
			И ДанныйТипДокументаМожноСогласовывать()
		Тогда
		
			Если ЭДОбъект.ResolutionStatus = Неопределено Тогда
				
				//Такой кейс возможен в двух случаях:
				//1. По документу еще не запрашивали согласование
				//2. Документ был согласован, далее подписан и отправлен. После этого ResolutionStatus возвращается Неопределено, как будто бы и не согласовывался. 
				
				Возврат МетодКлиента("Модуль_Клиент", "ДокументНеСогласованРанее", ЭДОбъект);
				
			ИначеЕсли 	ЭДОбъект.ResolutionStatus.Type = "ApprovementRequested" 	  		// Документ в состоянии согласования
				 		И (ЭДОбъект.ResolutionStatus.TargetUser = Неопределено    	  		// Согласовать может любой пользователь
					 		ИЛИ ЭДОбъект.ResolutionStatus.TargetUser.Id = MyUser.Id) Тогда	// Согласовать может текущий пользователь
							
				Возврат Истина;
			Иначе
				Возврат Ложь;			
			КонецЕсли;
			
		Иначе
			Возврат Ложь;
		КонецЕсли;
				
	КонецФункции
	
	&НаКлиенте
	Функция ДанныйТипДокументаМожноСогласовывать()
		
		Возврат ЭДОбъект.Type = "XmlTorg12"					// Формализованный документ с кастомной печатной формой
			ИЛИ ЭДОбъект.Type = "XmlAcceptanceCertificate"	// Формализованный документ с кастомной печатной формой
			ИЛИ ЭДОбъект.Type = "NonformalizedProforma"
			ИЛИ ЭДОбъект.Type = "Nonformalized"
			ИЛИ ЭДОбъект.Type = "ServiceDetails"
			ИЛИ ЭДОбъект.Type = "PriceListAgreement"
			ИЛИ ЭДОбъект.Type = "ReconciliationAct"
			ИЛИ ЭДОбъект.Type = "CertificateRegistry"
			ИЛИ ЭДОбъект.Type = "PriceList"
			ИЛИ ЭДОбъект.Type = "NonformalizedAcceptanceCertificate"
			ИЛИ ЭДОбъект.Type = "Contract";
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуПередачиНаСогласование(UserPermissions)
		
		Если ТочкаВызова = "ТаблицаДокументовИсходящих" Тогда
			
			Возврат Ложь;
			
		ИначеЕсли НЕ ЭДОбъект = Неопределено
			И UserPermissions.CanRequestResolutions 		// могу запросить согласование или подписание
			И ( ЭДОбъект.Type = "XmlTorg12"					// может приходить формализованный документ, но с кастомной печатной формой  
			ИЛИ ЭДОбъект.Type = "XmlAcceptanceCertificate" 	// может приходить формализованный документ, но с кастомной печатной формой  
			ИЛИ ЭДОбъект.Type = "NonformalizedProforma"
			ИЛИ ЭДОбъект.Type = "Nonformalized"
			ИЛИ ЭДОбъект.Type = "ServiceDetails"
			ИЛИ ЭДОбъект.Type = "PriceListAgreement"
			ИЛИ ЭДОбъект.Type = "ReconciliationAct"
			ИЛИ ЭДОбъект.Type = "CertificateRegistry"
			ИЛИ ЭДОбъект.Type = "PriceList"
			ИЛИ ЭДОбъект.Type = "NonformalizedAcceptanceCertificate"
			ИЛИ ЭДОбъект.Type = "Contract")
			И ЭДОбъект.ResolutionStatus = Неопределено
		Тогда
		
			Возврат Истина;
		КонецЕсли; 
				
		Возврат Ложь;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуПередачиНаПодпись(UserPermissions)
		
		Если ТочкаВызова = "ТаблицаДокументовИсходящих" Тогда
			
			Возврат Ложь;
			
		ИначеЕсли НЕ ЭДОбъект = Неопределено
			И UserPermissions.CanRequestResolutions //могу запросить согласование или подписание
			И ЭДОбъект.ResolutionStatus = Неопределено
			И ЭДОбъект.Status = "InboundWaitingForRecipientSignature"
			И ( ЭДОбъект.Type = "XmlTorg12" 
			ИЛИ ЭДОбъект.Type = "XmlAcceptanceCertificate"
			ИЛИ ЭДОбъект.Type = "NonformalizedProforma"
			ИЛИ ЭДОбъект.Type = "Nonformalized"
			ИЛИ ЭДОбъект.Type = "ServiceDetails"
			ИЛИ ЭДОбъект.Type = "PriceListAgreement"
			ИЛИ ЭДОбъект.Type = "ReconciliationAct"
			ИЛИ ЭДОбъект.Type = "CertificateRegistry"
			ИЛИ ЭДОбъект.Type = "PriceList"
			ИЛИ ЭДОбъект.Type = "NonformalizedAcceptanceCertificate"
			ИЛИ ЭДОбъект.Type = "Contract")
			
		Тогда
		
			Возврат Истина;
		КонецЕсли;
						
		Возврат Ложь;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуУдалить(UserPermissions)
		
		Если НЕ ЭДОбъект = Неопределено
			И НЕ ЭДОбъект.IsDeleted 
			И (UserPermissions.CanSignIncomingDocuments) Тогда
			Возврат Истина;
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуСоздатьДокумент1С()
		
		Если НЕ ЭДОбъект = Неопределено И МетодКлиента("Модуль_Клиент","ЭтоНеформализованныйДокументБезМетаданных", ЭДОбъект) = Ложь
			И НЕ ЗначениеЗаполнено(Документ1С) Тогда
			
			Возврат Истина;
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуЗапросаАннулирования(UserPermissions)
		
		Если НЕ ЭДОбъект = Неопределено 
			И UserPermissions.CanSignIncomingDocuments 
			И ЭДОбъект.Organization.AuthenticateType = "Certificate" Тогда
			Если (ЭДОбъект.Status = "InboundWithRecipientSignature" 
				ИЛИ ЭДОбъект.Status = "InboundRecipientSignatureRequestRejected"
				ИЛИ ЭДОбъект.Status = "OutboundWithRecipientSignature" 
				ИЛИ ЭДОбъект.Status = "OutboundRecipientSignatureRequestRejected"
				ИЛИ ЭДОбъект.Status = "OutboundWaitingForRecipientSignature"
				ИЛИ ЭДОбъект.Status = "OutboundWaitingForReceipt"
				ИЛИ ЭДОбъект.Status = "OutboundWaitingForSenderSignature"
				ИЛИ ЭДОбъект.Status = "InboundFinished"
				ИЛИ ЭДОбъект.Status = "OutboundFinished"
				ИЛИ ЭДОбъект.Status = "Outbound"
				ИЛИ ЭДОбъект.Status = "Inbound")
				И (ЭДОбъект.RevocationStatus = "RevocationStatusNone"
				ИЛИ ЭДОбъект.RevocationStatus = "RevocationRejected") Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецФункции

	&НаКлиенте
	Функция ПоказатьКнопкуАннулирования(UserPermissions)
		
		Если НЕ ЭДОбъект = Неопределено Тогда
			Если UserPermissions.CanSignIncomingDocuments И ЭДОбъект.Organization.AuthenticateType = "Certificate" Тогда
				Если ЭДОбъект.RevocationStatus = "RequestsMyRevocation" Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецФункции

	&НаКлиенте
	Функция ПоказатьКнопкуОтказаАннулирования(UserPermissions)
		
		Если НЕ ЭДОбъект = Неопределено Тогда
			Если UserPermissions.CanSignIncomingDocuments И ЭДОбъект.Organization.AuthenticateType = "Certificate" Тогда
				Если ЭДОбъект.RevocationStatus = "RequestsMyRevocation" Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецФункции

	&НаКлиенте
	Функция ПоказатьКнопкуЗапросаУточнения(ЭДОбъект, UserPermissions)
		
		Если НЕ ЭДОбъект = Неопределено И МетодКлиента("Модуль_Клиент", "ЭтоНеформализованныйДокументБезМетаданных", ЭДОбъект) = Ложь Тогда
			Если ЭДОбъект.Direction = "Inbound" Тогда
				Если (ЛЕВ(ЭДОбъект.Type, 7) = "Invoice" И ЭДОбъект.Organization.AuthenticateType = "Certificate")
					И (НЕ ЭДОбъект.Corrected 
					И НЕ ЭДОбъект.Revised 
					И НЕ ЭДОбъект.AmendmentRequested 
					И (ЭДОбъект.Status = "InboundFinished")) Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуПодписатьЗапрошенный(Document, UserPermissions, MyUser)
		
		Если ТочкаВызова = "ТаблицаДокументовИсходящих" ИЛИ ТочкаВызова = "ТаблицаДокументовВходящих" Тогда
			Возврат Ложь;
		КонецЕсли;
				
		Если НЕ Document = Неопределено И НЕ Document.ResolutionStatus = Неопределено Тогда
		
			//1. Документ отправлен на подпись мне
			Если Document.ResolutionStatus.type = "SignatureRequested" И UserPermissions.CanSignDocuments = Истина Тогда
				
				Если Document.ResolutionStatus.TargetUser = Неопределено Тогда
					Возврат Истина;
				ИначеЕсли Document.ResolutionStatus.TargetUser.Id = MyUser.Id Тогда
					Возврат Истина;
				Иначе
					Возврат Ложь;
				КонецЕсли;
							
			КонецЕсли;
			
			//2. Документ уже согласован
			Если  Document.ResolutionStatus.type = "Approved" 		    
				И UserPermissions.CanSignDocuments = Истина          
			Тогда
				Возврат Истина;
			КонецЕсли;
			
			Возврат Ложь;
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецФункции
		
	&НаКлиенте
	Процедура НастроитьКнопкиКоманднойПанели()
		
		Если НЕ ЭДОбъект = Неопределено Тогда
			UserPermissions = ЭДОбъект.Organization.GetUserPermissions();
		КонецЕсли;
		
		MyUser = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetMyUser();
		
		МассивНастройкиВидимости = Новый Массив;
		
	    Если ПоказатьКнопкуПерейтиВДиадок() Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПерейтиВДиадок", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПерейтиВДиадок", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуСтруктураПодчиненности() Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ФормаСтруктураПодчиненности", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ФормаСтруктураПодчиненности", Ложь, Ложь);
		КонецЕсли;
				
		Если ПоказатьКнопкуОтправитьСвязанныеДокументы() Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаОтправитьСвязанныйДокумент", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаОтправитьСвязанныйДокумент", Ложь, Ложь);
		КонецЕсли;
				
		Если ПоказатьГруппуКнопокПодписания(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаКнопокПодписания", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаКнопокПодписания", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуОтменитьСопоставление() Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаОтменитьСопоставление", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаОтменитьСопоставление", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьГруппуКнопокСогласования(UserPermissions, MyUser) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаСогласование", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаСогласование", Ложь, Ложь);
		КонецЕсли;
		
		НужноПоказатьКнопкуПередачиНаСогласование= 	ПоказатьКнопкуПередачиНаСогласование(UserPermissions);
		НужноПоказатьКнопкуПередачиНаПодпись= 		ПоказатьКнопкуПередачиНаПодпись(UserPermissions);
		
		Если НужноПоказатьКнопкуПередачиНаСогласование И НужноПоказатьКнопкуПередачиНаПодпись Тогда
			
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаПередачаНаСогласование", Истина, Истина);
		
		ИначеЕсли НужноПоказатьКнопкуПередачиНаСогласование И НЕ НужноПоказатьКнопкуПередачиНаПодпись Тогда
			
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаПередачаНаСогласование", 	Истина,	Истина);	
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаСогласование", 	Истина,	Истина);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаПодписание", 		Ложь, 	Ложь);
			
		ИначеЕсли НЕ НужноПоказатьКнопкуПередачиНаСогласование И НужноПоказатьКнопкуПередачиНаПодпись Тогда
			
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаПередачаНаСогласование", 	Истина,	Истина);	
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаСогласование", 	Ложь,	Ложь);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаПодписание", 		Истина, Истина);
			
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаПередачаНаСогласование", Ложь, Ложь);
		КонецЕсли;
		
		Если НужноПоказатьКнопкуПередачиНаСогласование Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаСогласование", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаСогласование", Ложь, Ложь);
		КонецЕсли;
		
		Если НужноПоказатьКнопкуПередачиНаПодпись Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаПодписание", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаПодписание", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуУдалить(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаУдалить", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаУдалить", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуСоздатьДокумент1С() Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаСоздатьДокумент1С", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаСоздатьДокумент1С", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуЗапросаАннулирования(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаЗапроситьАннулирование", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаЗапроситьАннулирование", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуАннулирования(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаАннулировать", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаАннулировать", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуОтказаАннулирования(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаОтказатьВАннулировании", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаОтказатьВАннулировании", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуЗапросаУточнения(ЭДОбъект, UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаЗапроситьУточнение", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаЗапроситьУточнение", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуПодписатьЗапрошенный(ЭДОбъект, UserPermissions, MyUser) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПодписатьЗапрошенный", Истина, Истина);	
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПодписатьЗапрошенный", Ложь, Ложь);	
		КонецЕсли;
		
		НастроитьВидимостьЭлементовФормыНаСервере(МассивНастройкиВидимости);
				
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ДобавитьНастройкуВидимости(МассивНастройкиВидимости, ИмяРеквизита, Видимость, Доступность)
		
		НастройкаВидимости = Новый Структура("ИмяРеквизита, Видимость, Доступность", ИмяРеквизита, Видимость, Доступность);
		МассивНастройкиВидимости.Добавить(НастройкаВидимости);
		
	КонецПроцедуры
	
	&НаСервере
	Процедура НастроитьВидимостьЭлементовФормыНаСервере(МассивНастройкиВидимости)
		
		Для каждого НастройкаВидимости из МассивНастройкиВидимости Цикл
			
			ИмяРеквизита 		= НастройкаВидимости.ИмяРеквизита;
			НастройкаВидимость 	= НастройкаВидимости.Видимость;
			НастройкаДоступность= НастройкаВидимости.Доступность;
			
			Элементы[ИмяРеквизита].Видимость 	= НастройкаВидимость;
			Элементы[ИмяРеквизита].Доступность 	= НастройкаДоступность;
			
		КонецЦикла;
		
	КонецПроцедуры
		
	&НаКлиенте
	Процедура НастроитьЭлементыФормы()
		
		Если ЭДОбъект <> Неопределено Тогда

			Если ЭДОбъект.Direction = "Inbound" Тогда

				МассивСсылокРНК= МетодКлиента("Модуль_Клиент","ПолучитьМассивСсылокРНКПоСчетуФактуреПолученномуДиадок", ЭДОбъект);
				ПодходящаяСФ=	 МетодКлиента("Модуль_Клиент","ПолучитьПодходящуюСФ", МассивСсылокРНК);
				
				ПредставлениеКнопкиСоздать=	МетодКлиента("Модуль_Клиент","СформироватьПредставлениеКнопкиСоздатьДокументВ1С", Документ1С, ЭДОбъект, МассивСсылокРНК, ПодходящаяСФ);
				Если ЗначениеЗаполнено(ПредставлениеКнопкиСоздать) Тогда
					Элементы.КнопкаСоздатьДокумент1С.Заголовок=	ПредставлениеКнопкиСоздать;
				КонецЕсли;
				
			КонецЕсли;
			
			Если МетодКлиента("Модуль_Клиент","ЭтоНеформализованныйДокументБезМетаданных", ЭДОбъект) Тогда
				Элементы.Номер.Видимость=	Ложь;
				Элементы.Сумма.Видимость=	Ложь;
			КонецЕсли; 
			
			Элементы.ДекорацияПодробно.Видимость= ЭДОбъект.Resolutions.Count > 0;
			
			Если ЗначениеЗаполнено(Документ1С) Тогда
				Элементы.ДокументВ1С.Заголовок= Новый ФорматированнаяСтрока(Строка(Документ1С),,,,ПолучитьНавигационнуюСсылку(Документ1С));
			КонецЕсли;
			
			Элементы.РамкаСтатуса.Видимость= Истина;

			ПредставлениеПодразделения=	?(ЭДОбъект.Department = Неопределено, "Головное подразделение", ЭДОбъект.Department.Name);
			
		Иначе
			
			Элементы.РамкаСтатуса.Видимость= Ложь;
			
		КонецЕсли;
		
		ЭтаФорма.Элементы.КнопкаПерейтиВДиадок.Заголовок = "Перейти в " + Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы;
				
	КонецПроцедуры
	
	&НаКлиенте
	Процедура УправлениеФормой()
		
		ОбновитьСтатусЭД();
		НастроитьЗаголовок();
		НастроитьКнопкиКоманднойПанели();
		НастроитьЭлементыФормы();
		
	КонецПроцедуры	 
	
	&НаКлиенте
	Функция ОбновитьСтатусЭД()
		
		Если НЕ ЭДОбъект = Неопределено Тогда
			
			ПредставлениеСтатуса=		 МетодКлиента("Модуль_Клиент","ПредставлениеСтатуса"			, ЭДОбъект);
			ПредставлениеСогласования=	 МетодКлиента("Модуль_Клиент","ПредставлениеСтатусаСогласования", ЭДОбъект);
			
			ПредставлениеСтатусаРоуминг= МетодКлиента("Модуль_Клиент","ПредставлениеСтатусаРоуминг"		, ЭДОбъект);
			ПредставлениеСтатусаРоумингДетали= ?(ЭДОбъект.RoamingNotificationStatus = "RoamingNotificationStatusError", ЭДОбъект.RoamingNotificationStatusDescription, "");
			
			СводныйСтатус= Новый Массив;
			Разделитель= "";
			
			Если НЕ ПустаяСтрока(ПредставлениеСтатуса) Тогда
				СводныйСтатус.Добавить(Разделитель);
				СводныйСтатус.Добавить(ПредставлениеСтатуса);
				Разделитель= " ";
			КонецЕсли;
			
			Если НЕ ПустаяСтрока(ПредставлениеСогласования) Тогда
				СводныйСтатус.Добавить(Разделитель);
				СводныйСтатус.Добавить(ПредставлениеСогласования);
				Разделитель= ". ";
			КонецЕсли;
			
			Если НЕ ПустаяСтрока(ПредставлениеСтатусаРоуминг) Тогда
				СводныйСтатус.Добавить(Разделитель);
				СводныйСтатус.Добавить(ПредставлениеСтатусаРоуминг);
				Разделитель= ". ";
			КонецЕсли;
			
			Элементы.СтатусДокумента.Заголовок=	Новый ФорматированнаяСтрока(СводныйСтатус); 
			
		Иначе
			
			Элементы.СтатусДокумента.Заголовок=	""; 
			
			ПредставлениеСтатусаРоуминг= 		"";
			ПредставлениеСтатусаРоумингДетали=  "";
			
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Процедура НастроитьЗаголовок()
		
		Заголовок=	МетодКлиента("Модуль_Клиент","ПредставлениеЭД", ЭДОбъект);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПросмотрФормы()
		
		СтруктураПредставленияЭД=	МетодКлиента("Модуль_Клиент","ПолучитьСтруктуруПредставленияЭД", ЭДОбъект);
		ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураПредставленияЭД);
		
		Если Дата<'20000101' тогда 
			дата = "";
		КонецЕсли;	
			
		ПоказыватьПанельПредупрежденияОСтаромФормате=
			ЛЕВ(ЭДОбъект.Type, 7) = "Invoice" И МетодКлиента("Модуль_ЛогикаПоведениеФорм","ПредупреждатьОСтаромФормате", ЭДОбъект.ConfirmationDate, ЭДОбъект.GetContent().InvoiceVersion);
		
		Если НЕ ПоказыватьПанельПредупрежденияОСтаромФормате Тогда
			Элементы.ГруппаПредупреждениеОНовомФормате.Видимость = Ложь;
		КонецЕсли;
			
		Элементы.FileName.Заголовок=	ЭДОбъект.FileName;
		
		УправлениеФормой();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПриОткрытии(Отказ)
		
		ПлатформаПриОткрытии(Отказ);
		
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			Организация= МетодКлиента("Модуль_Клиент", "НайтиОрганизациюВИерархииОрганизацийDiadoc", BoxID, DepartmentId);
		КонецЕсли;
		
		Organization= Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationById(BoxID);
		
		ПросмотрФормы();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПриЗакрытии()
		
		ПлатформаПриЗакрытии();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура FileNameНажатие(Элемент, СтандартнаяОбработка)
		
		Если ЭДОбъект.HasCustomPrintForm = Ложь Тогда
			ИмяВременногоФайла=	ПолучитьИмяВременногоФайла(РасширениеФайлаДиадок);
			ЭДОбъект.SaveSenderContent(ИмяВременногоФайла);
		Иначе
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");
			ЭДОбъект.GetPrintForm(ИмяВременногоФайла, 30);
		КонецЕсли;
		
		ЗапуститьПриложение(ИмяВременногоФайла);
		
	КонецПроцедуры
	
//} УПРАВЛЕНИЕ ФОРМОЙ И ОБРАБОТКА СОБЫТИЙ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ ОБРАБОТКА СОБЫТИЙ
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВыбораПодразделенияОрганизации(РезультатЗакрытия, ТекущийDepartmentId) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда 
			Если НЕ РезультатЗакрытия.DepartmentID = ТекущийDepartmentId Тогда
				
				ЭДОбъект.Move(РезультатЗакрытия.DepartmentID);
				
				ОбновитьЭДОбъект();
				
				ПараметрыОповещения=	Новый Структура;
				ПараметрыОповещения.Вставить("BoxID", 			ЭДОбъект.OrganizationID);
				ПараметрыОповещения.Вставить("DocumentId", 		ЭДОбъект.DocumentID);
				ПараметрыОповещения.Вставить("DepartmentName", 	?(ЭДОбъект.Department = Неопределено, "", ЭДОбъект.Department.Name));
				
				МетодКлиента(,"ОповеститьФормы", "ИзменениеПодразделения", ПараметрыОповещения);
				
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВыбораПолучателя(ПараметрыОтправкиНаСогласование, ТекущиеДанные) Экспорт
		
		МетодКлиента("Модуль_Клиент","ОтправитьНаОбработку", ЭДОбъект, ПараметрыОтправкиНаСогласование);
		ОбновитьЭДОбъект();
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыПодписанияДокумента(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			Если РезультатЗакрытия.ДокументПодписан = Истина Тогда
				
				Попытка  
					
					Если МетодКлиента("Модуль_Клиент","ЭтоНеформализованныйДокументБезМетаданных", ЭДОбъект) Тогда
						ПодписатьВДиадоке(Истина, ЭДОбъект.OrganizationID, ЭДОбъект.documentID);
						
					ИначеЕсли ЭДОбъект.Type = "UniversalTransferDocument" Тогда
						
						ПараметрыТитулаПолучателя = РезультатЗакрытия.ТитулПокупателя;
						ПараметрыТитулаПолучателя.Вставить("BoxID", 				BoxID);
						ПараметрыТитулаПолучателя.Вставить("CertificateThumbprint", ЭДОбъект.Organization.Certificate.Thumbprint);
						
						BuyerTitleInfo=	СформироватьТитулПолучателя(ПараметрыТитулаПолучателя, ЭДОбъект.Type, ЭДОбъект.DocumentDate);
						ПодписатьВДиадоке(Истина, ЭДОбъект.OrganizationID, ЭДОбъект.documentID,, BuyerTitleInfo);
					
					Иначе
						
						ПараметрыТитулаПолучателя=	Новый Структура();
						ПараметрыТитулаПолучателя.Вставить("ДатаПолученияГруза", 	РезультатЗакрытия.ДатаПолученияГруза);
						ПараметрыТитулаПолучателя.Вставить("ФИОПодписанта", 		Лев(РезультатЗакрытия.ФИОПодписанта,100));
						ПараметрыТитулаПолучателя.Вставить("ДолжностьПодписанта", 	Лев(РезультатЗакрытия.ДолжностьПодписанта,50));
						ПараметрыТитулаПолучателя.Вставить("ИНН", 					ЭДОбъект.Organization.INN);
						ПараметрыТитулаПолучателя.Вставить("НаименованиеОрганизации", ЭДОбъект.Organization.Name);
						ПараметрыТитулаПолучателя.Вставить("НаименованиеКонтрагента", ЭДОбъект.Counteragent.Name);
						ПараметрыТитулаПолучателя.Вставить("BoxID", 				  BoxID);
						
						BuyerTitleInfo=	СформироватьТитулПолучателя(ПараметрыТитулаПолучателя, ЭДОбъект.Type, ЭДОбъект.DocumentDate);
						ПодписатьВДиадоке(Истина, ЭДОбъект.OrganizationID, ЭДОбъект.documentID,, BuyerTitleInfo);
					
					КонецЕсли;
										
					Элементы.ГруппаКнопокПодписания.Доступность=	Ложь;
					Элементы.ГруппаКнопокПодписания.Видимость= 		Истина;
					
					ОбновитьЭДОбъект();
					
				Исключение 
					СообщениеОбОшибкеДиадок(ОписаниеОшибки());
				КонецПопытки;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОтменитьСопоставление(РезультатВопроса, ДополнительныеПараметры) Экспорт
		
		Если РезультатВопроса = КодвозвратаДиалога.Да Тогда
			
			Установить_DocumentID_Для_Документ(Документ1С,,,, МетодКлиента("Модуль_Клиент", "ЭтоУПД_ТипаСЧФДОП", ЭДОбъект));
			ОчиститьCustomDocumentId(Документ1С);

			Документ1С=	Неопределено;
			
			ОбновитьДокумент1С();
			ПросмотрФормы();
			
			ПараметрыОповещения=	Новый Структура;
			ПараметрыОповещения.Вставить("ТипСущности", "Документ");
			ПараметрыОповещения.Вставить("BoxID", ЭДОбъект.OrganizationID);
			ПараметрыОповещения.Вставить("DocumentID", ЭДОбъект.DocumentID);
			ПараметрыОповещения.Вставить("Документ1С", Документ1С);
			
			МетодКлиента(,"ОповеститьФормы", "ИзменениеСвязиДД1С", ПараметрыОповещения, ЭтаФорма);
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаЗапроситьУточнение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			
			Попытка
				
				МетодКлиента("Модуль_Клиент", "ПроверитьСертификат", ЭДОбъект.OrganizationID);
				
				ЭДОбъект.SendCorrectionRequest(РезультатЗакрытия.Комментарий);
				
				ОбновитьЭДОбъект();
				
			Исключение
				СообщениеОбОшибкеДиадок(ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВводаНакладной(ПараметрыФормы, ДополнительныеПараметры) Экспорт
		
		Если НЕ ПараметрыФормы = Неопределено Тогда
			Если ЗначениеЗаполнено(ПараметрыФормы.Документ1С) Тогда
				ОбработчикСозданиеДокумента(ПараметрыФормы.Документ1С);
			Иначе
				СоздатьДокумент(ПараметрыФормы.Режим);
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВыбораДокумента(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			Если ЗначениеЗаполнено(РезультатЗакрытия.Документ1С) Тогда
				ОбработчикСозданиеДокумента(РезультатЗакрытия.Документ1С);
			Иначе
				СоздатьДокумент(РезультатЗакрытия.Режим);
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВыбораДокументаСОснованием(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если РезультатЗакрытия <> Неопределено Тогда
			Если РезультатЗакрытия.СписокСсылокРНК.Количество() > 0 Тогда
				МетодКлиента("Модуль_ЛогикаПоведениеФорм","СоздатьНовыйСчетФактуру", ЭтаФорма, Контрагент, Организация, ЭДОбъект, РезультатЗакрытия.СписокСсылокРНК.ВыгрузитьЗначения());
			Иначе
				СоздатьДокумент(РезультатЗакрытия.Режим);
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикЗакрытиеФормыСФ(РезультатЗакрытия, ДокументОбъект) Экспорт
		
		Если ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
			ОбработчикСозданиеДокумента(ДокументОбъект.Ссылка);
		КонецЕсли;
		
		МассивСсылокРНК=	Новый Массив;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ОбработчикСозданиеДокумента(ДокументССылка) Экспорт
		
		Если ЗначениеЗаполнено(ДокументССылка) Тогда
			МетодКлиента("Модуль_Клиент","ОбработчикСозданиеДокумента", ДокументССылка, ЭДОбъект);
			ОбновитьДокумент1С();
			ПросмотрФормы();
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикУдалитьДокумент(РезультатВопроса, ДополнительныеПараметры) Экспорт
		
		Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
			
			ПараметрУдаляемогоДокумента=	Новый Структура();
			ПараметрУдаляемогоДокумента.Вставить("DocumentId", 	ЭДОбъект.DocumentId);
			ПараметрУдаляемогоДокумента.Вставить("BoxID", 		ЭДОбъект.Organization.Id);
			
			МассивДокументов=	Новый Массив();
			МассивДокументов.Добавить(ПараметрУдаляемогоДокумента);
			
			Если ЗначениеЗаполнено(Документ1С) Тогда
				Установить_DocumentID_Для_Документ(Документ1С,,, ЭДОбъект.Type = "NonformalizedProforma", МетодКлиента("Модуль_Клиент", "ЭтоУПД_ТипаСЧФДОП", ЭДОбъект));
				ОчиститьCustomDocumentId(Документ1С, ЭДОбъект.Type = "NonformalizedProforma");
			КонецЕсли;
			
			Если НЕ ЭДОбъект.IsDeleted Тогда
				Попытка
					ЭДОбъект.Delete();
					ПоказатьПредупреждение(, "Документ " + МетодКлиента("Модуль_Клиент","ПредставлениеЭД", ЭДОбъект) + " перемещен в удаленные.", 120, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
				Исключение
					
					Подробно=	ОписаниеОшибки();
					ОписаниеОшибки=	ОписаниеОшибки;
					Если Найти(ОписаниеОшибки, "is already delete") Тогда
						ОписаниеОшибки=	"Документ " + МетодКлиента("Модуль_Клиент","ПредставлениеЭД", ЭДОбъект) + " уже был удален.";
					Иначе
						ОписаниеОшибки=	"Ошибка удаления документа";
					КонецЕсли;
					
					ПараметрыФормы=	Новый Структура();
					ПараметрыФормы.Вставить("Заголовок", 		"Ошибка удаления");
					ПараметрыФормы.Вставить("ОписаниеОшибки", 	ОписаниеОшибки);
					ПараметрыФормы.Вставить("Подробности", 		Подробно);
					
					МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы, ЭтаФорма);
					
				КонецПопытки;
				
			КонецЕсли;
			
			ПросмотрФормы();
			МетодКлиента(,"ОповеститьФормы", "УдалениеДокументов", МассивДокументов);
			
		КонецЕсли;
		
	КонецПроцедуры

//} ОБРАБОТКА СОБЫТИЙ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ КОМАНДЫ
	
	&НаКлиенте
	Процедура Подписать(Команда)
		
		ФИОПодписанта = МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьФИОПодписанта", Organization);
						
		ПараметрыФормы=		Новый Структура;
		ПараметрыФормы.Вставить("ФИО", 						ФИОПодписанта);
		ПараметрыФормы.Вставить("ПредставлениеДокумента", 	Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи", 	Organization.Name+ ?(ПустаяСтрока(ФИОПодписанта), "", ", " + ФИОПодписанта));
		ПараметрыФормы.Вставить("Организация", 				ПолучитьСвязаннуюОрганизацию(Organization.ID));
		ПараметрыФормы.Вставить("ЭДОбъектType", 			ЭдОбъект.Type);
				
		Если ЭдОбъект.Type = "UniversalTransferDocument" Тогда
			ПараметрыФормы.Вставить("OrganizationName", 	ЭдОбъект.Organization.Name);
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаПодписанияДокументаУПД", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыПодписанияДокумента");
		Иначе
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаПодписанияДокумента", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыПодписанияДокумента");
        КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОтказатьВПодписи(Команда)
		
		ФИОПодписанта = МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьФИОПодписанта", Organization);
						
		ПараметрыФормы=		Новый Структура;
		ПараметрыФормы.Вставить("РежимИспользования", 		1);
		ПараметрыФормы.Вставить("ПредставлениеДокумента", 	Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи", 	Organization.Name+ ?(ПустаяСтрока(ФИОПодписанта), "", ", " + ФИОПодписанта));
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОтветаПоДокументу", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыОтветаОтказаВПодписи");

	КонецПроцедуры
	
	&НаКлиенте
	Процедура Согласовать(Команда)
		
		ФИОПодписанта=	?(НЕ Organization.Certificate = Неопределено,  Organization.Certificate.Name, "");
		
		ПараметрыФормы=		Новый Структура;
		ПараметрыФормы.Вставить("РежимИспользования", 		2);
		ПараметрыФормы.Вставить("AuthenticateType", 		Organization.AuthenticateType);
		ПараметрыФормы.Вставить("ПредставлениеДокумента", 	Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи", 	Organization.Name+ ?(ПустаяСтрока(ФИОПодписанта), "", ", " + ФИОПодписанта));
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОтветаПоДокументу", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыОтветаСогласовать");

	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОтказатьВСогласовании(Команда)
		
		ФИОПодписанта=	?(НЕ Organization.Certificate = Неопределено,  Organization.Certificate.Name, "");
		
		ПараметрыФормы=		Новый Структура;
		ПараметрыФормы.Вставить("РежимИспользования", 		3);
		ПараметрыФормы.Вставить("ПредставлениеДокумента", 	Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи", 	Organization.Name+ ?(ПустаяСтрока(ФИОПодписанта), "", ", " + ФИОПодписанта));
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОтветаПоДокументу", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыОтветаОтказатьВСогласовании");

	КонецПроцедуры
	
	&НаКлиенте
	Процедура Удалить(Команда)
		
		Оповещение=	Новый ОписаниеОповещения("ОбработчикУдалитьДокумент", ЭтаФорма);
		ПоказатьВопрос(Оповещение, "Вы действительно хотите удалить документ?", РежимДиалогаВопрос.ДаНет, 120, КодВозвратаДиалога.Нет, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы, КодВозвратаДиалога.Нет);

	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОтменитьСопоставление(Команда)
		
		Оповещение=	Новый ОписаниеОповещения("ОбработчикОтменитьСопоставление", ЭтаФорма);
		ПоказатьВопрос(Оповещение, "Вы действительно хотите отменить сопоставление с документом 1С?", РежимДиалогаВопрос.ДаНет, 120, КодВозвратаДиалога.Нет, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы, КодВозвратаДиалога.Нет);

	КонецПроцедуры
	
	&НаКлиенте
	Процедура СтруктураПодчиненности(Команда)
		
		ПараметрыФормы=	Новый Структура("Режим", "СтруктураПодчиненности");
		
		ФормаСвязейДокументов= МетодКлиента(,"ПолучитьФормуОбработки", "ФормаСвязейДокументов", ПараметрыФормы, ЭтаФорма, СокрЛП(ЭДОбъект.DocumentID) + "/" + СокрЛП(ЭДОбъект.OrganizationID));
		ФормаСвязейДокументов.ЭДОбъект=		ЭДОбъект;
		ФормаСвязейДокументов.Organization=	Organization;
		
		ОткрытьФорму(ФормаСвязейДокументов);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ДекорацияПодробноНажатие(Элемент)
		
		HTMLДокумент= МетодКлиента("Модуль_Клиент","СформироватьHTMLПредставлениеРезолюций", ЭДОбъект, Организация);
		ПараметрыФормы=	Новый Структура("HTMLДокумент",	HTMLДокумент);
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыводаHTMLДокумента", ПараметрыФормы, ЭтаФорма);

	КонецПроцедуры
	
	&НаКлиенте
	Процедура СтатусДокументаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
		
		Если НавигационнаяСсылкаФорматированнойСтроки = "RoamingNotificationStatusError" Тогда
			
			СтандартнаяОбработка= Ложь;
			
			Если НРег(Лев(ПредставлениеСтатусаРоуминг, 6)) = "ошибка" Тогда
				
				ПараметрыФормы= Новый Структура("Заголовок, ОписаниеОшибки, Подробности",
				ПредставлениеСтатусаРоуминг,
				ПредставлениеСтатусаРоуминг,
				ПредставлениеСтатусаРоумингДетали);
				
				МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы, ЭтаФорма);
				
			Иначе
				ОбновитьСтатусЭД();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПередатьНаСогласование(Команда)
		
		МетодКлиента("Модуль_Клиент","ОтправитьЭДОбъектНаСогласование", ЭтаФорма, ЭтаФорма, "ApprovementRequest", "ПередачаНаОбработку");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПередатьНаПодписание(Команда)
		
		МетодКлиента("Модуль_Клиент","ОтправитьЭДОбъектНаСогласование", ЭтаФорма, ЭтаФорма, "SignatureRequest", "ПередачаНаОбработку");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗапроситьАннулирование(Команда)
		
		ФИОПодписанта = МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьФИОПодписанта", Organization);
						
		ПараметрыФормы=		Новый Структура;
		ПараметрыФормы.Вставить("РежимИспользования", 		5);
		ПараметрыФормы.Вставить("ПредставлениеДокумента", 	Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи", 	Organization.Name+ ?(ПустаяСтрока(ФИОПодписанта), "", ", " + ФИОПодписанта));
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОтветаПоДокументу", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыОтветаЗапросАннулирования");

	КонецПроцедуры

	&НаКлиенте
	Процедура АннулироватьДокумент(Команда)
		
		ФИОПодписанта = МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьФИОПодписанта", Organization);
		
		ПараметрыФормы=		Новый Структура;
		ПараметрыФормы.Вставить("РежимИспользования", 		1);
		ПараметрыФормы.Вставить("ПредставлениеДокумента", 	Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи", 	Organization.Name+ ?(ПустаяСтрока(ФИОПодписанта), "", ", " + ФИОПодписанта));
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОтветаПоДокументуБезКомментария", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыОтветаАннулироватьДокумент");

	КонецПроцедуры

	&НаКлиенте
	Процедура ОтказатьВАннулировании(Команда)
		
		ФИОПодписанта = МетодКлиента("Модуль_РаботаССерверомДиадок","ПолучитьФИОПодписанта", Organization);
				
		ПараметрыФормы=		Новый Структура;
		ПараметрыФормы.Вставить("РежимИспользования", 		6);
		ПараметрыФормы.Вставить("ПредставлениеДокумента", 	Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи", 	Organization.Name+ ?(ПустаяСтрока(ФИОПодписанта), "", ", " + ФИОПодписанта));
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОтветаПоДокументу", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыОтветаОтказатьВАннулировании");

	КонецПроцедуры

	&НаКлиенте
	Процедура СоздатьДокумент1С(Команда)
		
		Если ЛЕВ(ЭДОбъект.Type, 7) = "Invoice"  Тогда
			
			Если ЗначениеЗаполнено(ПодходящаяСФ) Тогда
				ОбработчикСозданиеДокумента(ПодходящаяСФ);
			ИначеЕсли МассивСсылокРНК.Количество() > 0 И ЭДОбъект.Type = "Invoice" Тогда
				МетодКлиента("Модуль_ЛогикаПоведениеФорм","СоздатьНовыйСчетФактуру", ЭтаФорма, Контрагент, Организация, ЭДОбъект, МассивСсылокРНК);
			Иначе
				СоздатьДокумент("ВводСВыборомОснований");
			КонецЕсли;
			
		Иначе
			СоздатьДокумент("Ввод");
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗапроситьУточнение(Команда)
		
		ФИОПодписанта = МетодКлиента("Модуль_РаботаССерверомДиадок","ПолучитьФИОПодписанта", Organization);
		
		ПараметрыФормы=		Новый Структура;
		ПараметрыФормы.Вставить("РежимИспользования", 		7);
		ПараметрыФормы.Вставить("ПредставлениеДокумента", 	Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи", 	Organization.Name+ ?(ПустаяСтрока(ФИОПодписанта), "", ", " + ФИОПодписанта));
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОтветаПоДокументу", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыОтветаЗапроситьУточнение");
		
	КонецПроцедуры
	
//} КОМАНДЫ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ ИНТЕГРАЦИЯ
	
	&НаКлиенте
	Процедура ПодписатьВДиадоке(Подписать, BoxID, DocumentID, ТекстОтказа = "", СтруктураДляТитула = Неопределено)
		
		Если НЕ ЭДОбъект = Неопределено Тогда
			МетодКлиента("Модуль_Клиент","ПодписатьВДиадоке", ЭДОбъект, Подписать, BoxID, DocumentID, ТекстОтказа, СтруктураДляТитула);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаОтказаВПодписи(РезультатЗакрытия, ТекущийDepartmentId) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			Попытка
				ПодписатьВДиадоке(Ложь, ЭДОбъект.OrganizationID, ЭДОбъект.documentID, РезультатЗакрытия.Комментарий); 
				Элементы.ГруппаКнопокПодписания.Доступность=	Ложь;
				Элементы.ГруппаКнопокПодписания.Видимость= 		Истина;
				ОбновитьЭДОбъект();
			Исключение
				СообщениеОбОшибкеДиадок(ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаСогласовать(РезультатЗакрытия, ТекущийDepartmentId) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			Попытка
				ЭДОбъект.Approve(РезультатЗакрытия.Комментарий);
				Элементы.ГруппаКнопокСогласования.Доступность=	Ложь;
				Элементы.ГруппаКнопокСогласования.Видимость= 	Истина;
				ОбновитьЭДОбъект();
			Исключение
				ТекстОшибкиПодписания=	ОписаниеОшибки();
				Если НЕ Найти(ТекстОшибкиПодписания, "Duplicate resolution") = 0 Тогда
					ПоказатьПредупреждение(, "Повторное согласование не возможно", 120, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
					Возврат;
				ИначеЕсли НЕ Найти(ТекстОшибкиПодписания, "User cannot add resolution") = 0 Тогда
					ПоказатьПредупреждение(, "Вы не можете согласовать документ", 120, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
					Возврат;
				КонецЕсли;
				СообщениеОбОшибкеДиадок(ТекстОшибкиПодписания);
			КонецПопытки;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаОтказатьВСогласовании(РезультатЗакрытия, ТекущийDepartmentId) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			Попытка
				ЭДОбъект.DisApprove(РезультатЗакрытия.Комментарий);
				ОбновитьЭДОбъект();
			Исключение
				ТекстОшибкиПодписания=	ОписаниеОшибки();
				Если НЕ Найти(ТекстОшибкиПодписания, "Duplicate resolution") = 0 Тогда
					ПоказатьПредупреждение(, "Повторный отказ в согласовании не возможен", 120, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
					Возврат;
				ИначеЕсли НЕ Найти(ТекстОшибкиПодписания, "User cannot add resolution") = 0 Тогда
					ПоказатьПредупреждение(, "Вы не можете отказать в согласовании", 120, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
					Возврат;
				КонецЕсли;
				СообщениеОбОшибкеДиадок(ТекстОшибкиПодписания);
			КонецПопытки;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаЗапросАннулирования(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			
			Попытка
				
				Если МетодКлиента("Модуль_Клиент", "ЭтоФормализованныйДокумент", ЭДОбъект.Type) Тогда
					МетодКлиента("Модуль_Клиент", "ПроверитьСертификат", ЭДОбъект.OrganizationID);
				КонецЕсли;
				
				ЭДОбъект.SendRevocationRequest(РезультатЗакрытия.Комментарий);
				
				ОбновитьЭДОбъект();
				
			Исключение
				СообщениеОбОшибкеДиадок(ОписаниеОшибки());
			КонецПопытки;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаАннулироватьДокумент(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			
			Попытка  
				
				Если МетодКлиента("Модуль_Клиент", "ЭтоФормализованныйДокумент", ЭДОбъект.Type) Тогда
					МетодКлиента("Модуль_Клиент", "ПроверитьСертификат", ЭДОбъект.OrganizationID);
				КонецЕсли;
				
				ЭДОбъект.AcceptRevocationRequest();
				
				ОбновитьЭДОбъект();
				
			Исключение 
				СообщениеОбОшибкеДиадок(ОписаниеОшибки());
			КонецПопытки;
			
		КонецЕсли;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаОтказатьВАннулировании(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			
			Попытка
				
				Если МетодКлиента("Модуль_Клиент", "ЭтоФормализованныйДокумент", ЭДОбъект.Type) Тогда
					МетодКлиента("Модуль_Клиент", "ПроверитьСертификат", ЭДОбъект.OrganizationID);
				КонецЕсли;
				
				ЭДОбъект.RejectRevocationRequest(РезультатЗакрытия.Комментарий);
				
				ОбновитьЭДОбъект();
				
			Исключение
				СообщениеОбОшибкеДиадок(ОписаниеОшибки());
			КонецПопытки;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПодразделениеНажатие(Элемент, СтандартнаяОбработка)
		
		СтандартнаяОбработка=	Ложь;
		
		ТекущийDepartmentId=	?(ЭДОбъект.Department = Неопределено, "", ЭДОбъект.Department.Id);
		
		ПараметрыФормы=	Новый Структура();
		ПараметрыФормы.Вставить("DepartmentId", 	ТекущийDepartmentId);
		ПараметрыФормы.Вставить("OrganizationId",	ЭДОбъект.Organization.Id);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ВыборПодразделенияОрганизации", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВыбораПодразделенияОрганизации", ТекущийDepartmentId);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОтправитьсвязанныйДокумент(Команда)

		Если ЗначениеЗаполнено(Контрагент)=Ложь тогда
			ПоказатьПредупреждение(, "С "+ЭДОбъект.Counteragent.Name+" не сопоставлено ни одной карточки контрагента в 1С", 120, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
			возврат
		КонецЕсли;		
		
		ПараметрыФормы=	Новый Структура;
		ПараметрыФормы.Вставить("InitialDocumentID", ЭДОбъект.DocumentID);
		
		ПараметрыФормы.Вставить("Организация", Организация);
		ПараметрыФормы.Вставить("ОписаниеКонтрагента", Новый структура("Контрагент, CounteragentName, CounteragentID",  Контрагент, ЭДОбъект.Counteragent.Name, ЭДОбъект.Counteragent.ID));
		
		Форма_Выгрузка = МетодКлиента(,"ОткрытьФормуОбработки", "Форма_Выгрузка", ПараметрыФормы, ЭтаФорма, СокрЛП(ЭДОбъект.DocumentID) + "/" + СокрЛП(ЭДОбъект.OrganizationID));

	КонецПроцедуры

	&НаКлиенте
	Процедура Декорация4Нажатие(Элемент)
		ТекстИнформацииОНовомФормате= МетодКлиента("Модуль_Клиент","ТекстИнформацииОНовомФормате");
		ПараметрыФормы= Новый Структура("HTMLДокумент", ТекстИнформацииОНовомФормате);
		МетодКлиента(,"ОткрытьФормуОбработки", "ФормаВыводаHTMLДокумента", ПараметрыФормы, ЭтаФорма);
	КонецПроцедуры

	&НаКлиенте
	Процедура ПодписатьЗапрошенный(Команда)
		
		ФИОПодписанта= 		МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьФИОПодписанта", Organization);
		СтруктураSigner= 	МетодСервера(, "ПолучитьДанныеПодписиСогласующим", Организация);
		
		Если НЕ ЗначениеЗаполнено(СтруктураSigner.ДолжностьПодписанта) Тогда
			ПараметрыФормы= Новый Структура();
			ПараметрыФормы.Вставить("ФИО", ФИОПодписанта);
			
			ДополнительныеПараметры= Новый Структура;
			ДополнительныеПараметры.Вставить("Document", ЭДОбъект);
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "НастройкаПодписиАкт", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыНастройкиПодписи_ПодписаниеЗапрошенного", ДополнительныеПараметры);
		Иначе
			ПодписатьИОтправить_ПодписаниеЗапрошенного(СтруктураSigner, ЭДОбъект);	
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ОбработчикОткрытиеФормыНастройкиПодписи_ПодписаниеЗапрошенного(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
			ФИОПодписанта= 		РезультатЗакрытия.ФИО; 
			СтруктураSigner= 	МетодСервера(, "ПолучитьДанныеПодписиСогласующим", Организация);
			ПодписатьИОтправить_ПодписаниеЗапрошенного(СтруктураSigner, ДополнительныеПараметры.Document);
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ПодписатьИОтправить_ПодписаниеЗапрошенного(СтруктураSigner, Document) 
		
		СтруктураРезультатОперации= МетодКлиента("Модуль_РаботаССерверомДиадок", "ПодписатьИОтправить_ПодписаниеЗапрошенного", СтруктураSigner, Document);
						
		Если СтруктураРезультатОперации.ВыполненоУспешно Тогда 
			ОбновитьЭДОбъект();
		Иначе
			ТекстОшибкиПодписания= СтруктураРезультатОперации.ТекстОшибки;
			
			ПараметрыФормы= Новый Структура();
			ПараметрыФормы.Вставить("Заголовок", 		"Ошибка при подписании документа");
			ПараметрыФормы.Вставить("ОписаниеОшибки", 	"Не удалось подписать документ");
			ПараметрыФормы.Вставить("Подробности", 		ТекстОшибкиПодписания+Символы.ПС+"BoxId: "+Document.OrganizationId+Символы.ПС+"DocumentId: "+Document.DocumentId);
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы, ЭтаФорма);
		КонецЕсли;
		
	КонецПроцедуры
		
//} ИНТЕГРАЦИЯ
////////////////////////////////////////////////////////////////////////////////