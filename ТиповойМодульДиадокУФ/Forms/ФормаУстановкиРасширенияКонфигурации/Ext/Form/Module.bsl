
#Область ПЕРМЕННЫЕ_ПЛАТФОРМЫ

&НаКлиенте
Перем Платформа Экспорт;

&НаСервере
Перем ОбработкаОбъект;

#КонецОбласти

#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ_ПЛАТФОРМЫ

&НаКлиенте
Функция МетодКлиента(ИмяМодуля= "", ИмяМетода, 
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL,
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат  Платформа.МетодКлиента(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаКлиенте
Функция МетодСервераБезКонтекста(ИмяМодуля= "", ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат Платформа.МетодСервераБезКонтекста(ИмяМодуля, ИмяМетода,
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция МетодСервера(Знач ИмяМодуля= "", Знач ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат ОбработкаОбъект().МетодСервера(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция ОбработкаОбъект() Экспорт
	
	Если ОбработкаОбъект = Неопределено Тогда
		
		СтруктураОбработки= ПолучитьИзВременногоХранилища(Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
		
		Если СтруктураОбработки <> Неопределено Тогда
			ОбработкаОбъект= СтруктураОбработки.ОбработкаОбъект;
		КонецЕсли;
		
		Если ОбработкаОбъект = Неопределено Тогда
			
			ОбработкаОбъект= РеквизитФормыВЗначение("Объект");
			
			Попытка
				ПоместитьВоВременноеХранилище(Новый Структура("ОбработкаОбъект", ОбработкаОбъект), Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
			Исключение КонецПопытки;
		
		Иначе
			ОбработкаОбъект.ПараметрыКлиентСервер= Объект.ПараметрыКлиентСервер;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОбработкаОбъект;
	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма(ТекущийВладелецФормы)
	
	Если ТекущийВладелецФормы = Неопределено Тогда
		Возврат Неопределено
	ИначеЕсли Прав(ТекущийВладелецФормы.ИмяФормы, 14) = "Форма_Основная" Тогда
		Возврат ТекущийВладелецФормы;
	Иначе
		Возврат ОсновнаяФорма(ТекущийВладелецФормы.ВладелецФормы);
	КонецЕсли;
	
КонецФункции


&НаСервере
Процедура ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", Объект.ПараметрыКлиентСервер);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатформаПриОткрытии(Отказ)
	
	ОсновнаяФорма= ОсновнаяФорма(ВладелецФормы);
	
	Если ОсновнаяФорма <> Неопределено Тогда
		Платформа= ОсновнаяФорма.Платформа;
	КонецЕсли;
		
	Платформа.ПриОткрытииФормыОбработки(ЭтаФорма, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатформаПриЗакрытии()
	
	Платформа.ПриЗакрытииФормыОбработки(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка);
	
	ТекущяяВерсияРасширенияДиадок = Параметры.ТекущяяВерсияРасширенияДиадок;
	
	Если ЗначениеЗаполнено(ТекущяяВерсияРасширенияДиадок) Тогда
		
		ТребуемаяВерсияРасширенияДиадок = МетодСервера(,"ТребуемаяВерсияРасширенияДиадок");
		
		ТекстСообщения = "<h4> В базе данных установлено расширение ""Диадок"" с версией " + ТекущяяВерсияРасширенияДиадок + ".</h4>"
							+ "<h4> Заменить его на расширение ""Диадок"" с версией " + ТребуемаяВерсияРасширенияДиадок + " ? </h4>"
							+ "<p> При замене расширения будут выполнены следующие действия: </p1>"
							+ "<p> 1. Расширение с версией " + ТекущяяВерсияРасширенияДиадок + " будет удалено. </p1>"
							+ "<p> 2. Расширение с версией " + ТребуемаяВерсияРасширенияДиадок + " будет установлено. </p1>";
							
		Элементы.ОК.Заголовок = "Заменить";
		
	Иначе
		
		ТекстСообщения = "<h4> Теперь отправлять документы в Диадок можно прямо из базы данных. Для этого необходимо установить расширение конфигурации. </h4>"
							+ "<p> При установке расширения будут выполнены следующие действия: </p1>"
							+ "<p> 1. В базу данных будет установлено расширение конфигурации. </p1>"
							+ "<p> 2. В дополнительные обработки будет добавлен текущий интеграционный модуль Диадок. Если в дополнительных обработках уже есть используемый интеграционный модуль, то обработке будет выставлен признак ""Отключена"" </p1>"
							+ "<p> 3. После окончания установки необходимо будет перезапустить 1С Предприятие. </p1>";
							
		Элементы.ОК.Заголовок = "Установить";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ПлатформаПриОткрытии(Отказ);

	HTMLДокумент = "<!DOCTYPE html PUBLIC """"-//W3C//DTD XHTML 1.0 Transitional//EN
	|http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd"""">
	|
	|<html xmlns=""""http://www.w3.org/1999/xhtml"""">
	|
	|<head>
	|<meta http-equiv=""""Content-Type"""" content=""""text/html; charset=utf-8""""/>
	|
	|<style>	
	|p {font-family: 'Times New Roman', Times, serif; font-size: 11pt; }
	|</style>
	|
	|</head>";
	
	
	HTMLДокумент = HTMLДокумент + "<body>" + ТекстСообщения + "</<body>";
		
	ПолеСообщения = HTMLДокумент;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ПлатформаПриЗакрытии();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ОчиститьНеНапоминатьДляТекущейВерсии();
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура БольшеНеНапоминать(Команда)
	
	ЗапомнитьНеНапоминатьДляТекущейВерсии();
	
	Закрыть(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗапомнитьНеНапоминатьДляТекущейВерсии()
	
	МетодСервера(,"УстановитьНастройкуПользователя", "ДиадокНеИспользоватьРасширениеВерсия", ТекущяяВерсияРасширенияДиадок);
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьНеНапоминатьДляТекущейВерсии()
	
	МетодСервера(,"УстановитьНастройкуПользователя", "ДиадокНеИспользоватьРасширениеВерсия", "");
	
КонецПроцедуры

