
&НаКлиенте
Перем ФормаДокумента Экспорт;
&НаКлиенте
Перем ЭДОбъект Экспорт;
&НаКлиенте
Перем СтруктураЭДОбъект Экспорт;
&НаКлиенте
Перем ТекущийДоговорКонтрагента Экспорт;
&НаКлиенте
Перем ТекущееСоглашениеКонтрагента Экспорт;

#Область ПЕРМЕННЫЕ_ПЛАТФОРМЫ

&НаКлиенте
Перем Платформа Экспорт;

&НаСервере
Перем ОбработкаОбъект;

#КонецОбласти

#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ_ПЛАТФОРМЫ

&НаКлиенте
Функция МетодКлиента(ИмяМодуля= "", ИмяМетода, 
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL,
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат  Платформа.МетодКлиента(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаКлиенте
Функция МетодСервераБезКонтекста(ИмяМодуля= "", ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат Платформа.МетодСервераБезКонтекста(ИмяМодуля, ИмяМетода,
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция МетодСервера(Знач ИмяМодуля= "", Знач ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат ОбработкаОбъект().МетодСервера(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция ОбработкаОбъект() Экспорт
	
	Если ОбработкаОбъект = Неопределено Тогда
		
		СтруктураОбработки= ПолучитьИзВременногоХранилища(Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
		
		Если СтруктураОбработки <> Неопределено Тогда
			ОбработкаОбъект= СтруктураОбработки.ОбработкаОбъект;
		КонецЕсли;
		
		Если ОбработкаОбъект = Неопределено Тогда
			
			ОбработкаОбъект= РеквизитФормыВЗначение("Объект");
			
			Попытка
				ПоместитьВоВременноеХранилище(Новый Структура("ОбработкаОбъект", ОбработкаОбъект), Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
			Исключение КонецПопытки;
		
		Иначе
			ОбработкаОбъект.ПараметрыКлиентСервер= Объект.ПараметрыКлиентСервер;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОбработкаОбъект;
	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма(ТекущийВладелецФормы)
	
	Если ТекущийВладелецФормы = Неопределено Тогда
		Возврат Неопределено
	ИначеЕсли Прав(ТекущийВладелецФормы.ИмяФормы, 14) = "Форма_Основная" Тогда
		Возврат ТекущийВладелецФормы;
	Иначе
		Возврат ОсновнаяФорма(ТекущийВладелецФормы.ВладелецФормы);
	КонецЕсли;
	
КонецФункции


&НаСервере
Процедура ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", Объект.ПараметрыКлиентСервер);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатформаПриОткрытии(Отказ)
	
	ОсновнаяФорма= ОсновнаяФорма(ВладелецФормы);
	
	Если ОсновнаяФорма <> Неопределено Тогда
		Платформа= ОсновнаяФорма.Платформа;
	КонецЕсли;
		
	Платформа.ПриОткрытииФормыОбработки(ЭтаФорма, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатформаПриЗакрытии()
	
	Платформа.ПриЗакрытииФормыОбработки(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
//{ СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
	
	&НаСервере
	Функция ЧислоИзXML(Значение)
		Если ПустаяСтрока(Значение) Тогда
			Возврат 0;
		Иначе
			Возврат Число(Значение);
		КонецЕсли;
	КонецФункции
	
	&НаСервере
	Процедура ЗаполнитьСтрокуДанными1С(СтрокаТЧ)
		
		Item=	СтрокаТЧ.XmlTorg12_Item.Получить(0).Значение;
		
		Если СокрЛП(ВидОперации) = "Объекты строительства" Тогда
			//типизируем пустой ссылкой на объекты строительства
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
				СтрокаТЧ.Номенклатура=		Справочники.ОбъектыСтроительства.ПустаяСсылка();
				СтрокаТЧ.ЕдиницаИзмерения=	"";
			Иначе
				СтрокаТЧ.Номенклатура=		Справочники.Номенклатура.ПустаяСсылка();
				СтрокаТЧ.ЕдиницаИзмерения=	"";
			КонецЕсли;
			
		Иначе
			
			//Сначала установим пустую ссылку на номенклатуру
			СтрокаТЧ.Номенклатура=	Справочники.Номенклатура.ПустаяСсылка();
			
			Если 	Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30"
				ИЛИ Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11"
				ИЛИ Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БГУ20"
				ИЛИ Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" Тогда
			
				СтрокаТЧ.Номенклатура=	ПолучитьНоменклатуруПоставщика(Контрагент, Item.Code, Item.Code, Item.Name);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) И ЗначениеЗаполнено(Item.Code) Тогда
				СтрокаТЧ.Номенклатура=	ПолучитьНоменклатуруПоАртикулу(Item.Code);
			КонецЕсли;
			
			Если ИспользоватьЕдиницуИзмерения И ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
				
				СтрокаGUID= МетодСервера(,"ПолучитьЗначениеСвойства", СтрокаТЧ.Номенклатура, МетодСервера(,"ИдентификаторСвойстваЕдиницаИзмеренияНоменклатуры"));
				Если ЗначениеЗаполнено(СтрокаGUID) Тогда
					
					
					Если МетодСервера(,"СуществуетОбъектМетаданных", "Справочник.ЕдиницыИзмерения") Тогда
						ИмяСправочника = "ЕдиницыИзмерения";
					ИначеЕсли МетодСервера(,"СуществуетОбъектМетаданных", "Справочник.УпаковкиЕдиницыИзмерения") Тогда
						ИмяСправочника = "УпаковкиЕдиницыИзмерения";
					ИначеЕсли МетодСервера(,"СуществуетОбъектМетаданных", "Справочник.КлассификаторЕдиницИзмерения") Тогда
						ИмяСправочника = "КлассификаторЕдиницИзмерения";
					Иначе
						ИмяСправочника= "";
					КонецЕсли;
					
					Если НЕ ПустаяСтрока(ИмяСправочника) Тогда
						
						ЕдиницаИзмерения= Вычислить("Справочники."+ИмяСправочника+".ПолучитьСсылку(Новый УникальныйИдентификатор(СтрокаGUID))");
						
						Если МетодСервера(,"СсылкаСуществует", ЕдиницаИзмерения) Тогда
							СтрокаТЧ.ЕдиницаИзмерения= ЕдиницаИзмерения;
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(СтрокаТЧ.ЕдиницаИзмерения) Тогда
					СтрокаТЧ.ЕдиницаИзмерения= ItemStruct_2_ЕдиницаИзмерения(Item, СтрокаТЧ.Номенклатура);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(Item.TaxRate) Тогда
			СтрокаТЧ.СтавкаНДС=	МетодСервера(,"ПолучитьСтавкуНДСДиадок", Item.TaxRate);
		Иначе
			СтрокаТЧ.СтавкаНДС= ВернутьСтавкуБезНДС();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
			Если НЕ СтрокаТЧ.Номенклатура.Метаданные().Реквизиты.Найти("Артикул") = Неопределено Тогда
				СтрокаТЧ.Артикул=	СтрокаТЧ.Номенклатура.Артикул;
			Иначе
				СтрокаТЧ.Артикул=	СтрокаТЧ.Номенклатура.Код;
			КонецЕсли;
		КонецЕсли;
		
		//ЗаполнитьСтрокуПоНоменклатуре(СтрокаТЧ);
		
	КонецПроцедуры
	
	&НаСервере
	Функция ВернутьСтавкуБезНДС()
		
		Если НЕ Метаданные.Справочники.Найти("СтавкиНДС") = Неопределено Тогда
			СтавкаБезНДС= Справочники.СтавкиНДС.НайтиПоНаименованию("Без НДС", Истина);
		Иначе
			СтавкаБезНДС= Перечисления.СтавкиНДС.БезНДС;	
		КонецЕсли;
		
		Возврат СтавкаБезНДС;
		
	КонецФункции
		
	&НаСервере
	Процедура УстановитьСоответствиеНоменклатуры()
		
		Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл
			
			Item=	СтрокаТЧ.XmlTorg12_Item.Получить(0).Значение;
			Если МетодСервера(,"СуществуетОбъектМетаданных", "Справочник.НоменклатураПоставщиков") Тогда
				УстановитьНоменклатуруПоставщика(СтрокаТЧ.Номенклатура, Контрагент, Item.Code, Item.NomenclatureArticle, Item.Name);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.ЕдиницаИзмерения) Тогда
				МетодСервера(,"УстановитьЗначениеСвойства", СтрокаТЧ.Номенклатура, МетодСервера(,"ИдентификаторСвойстваЕдиницаИзмеренияНоменклатуры"), , СокрЛП(СтрокаТЧ.ЕдиницаИзмерения.УникальныйИдентификатор()));
			КонецЕсли;
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаСервере
	Процедура УстановитьСоответствиеСоглашения(УстанавливаемоеСоглашение, СоглашениеСвойство)
		
		МетодСервера(,"УстановитьЗначениеСвойства", УстанавливаемоеСоглашение, "DDPact", "Соглашение", СоглашениеСвойство);
		
	КонецПроцедуры
	
	&НаСервере
	Процедура УстановитьСоответствиеДоговора(УстанавливаемыйДоговор, ДоговорСвойство)
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" И ЗначениеЗаполнено(Соглашение) Тогда
			УстановитьСоответствиеСоглашения(Соглашение, СокрЛП(Контрагент.УникальныйИдентификатор()));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(УстанавливаемыйДоговор) Тогда
			МетодСервера(,"УстановитьЗначениеСвойства", УстанавливаемыйДоговор, "DDContract", "Договор", ДоговорСвойство);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура УстановитьСчетаРасчетовСКонтргаентом(ФормаДокумента)
		
		ДокументОбъект=	ФормаДокумента.Объект;
		
		СчетаРасчетов= ПолучитьСчетаРасчетов(ДокументОбъект.Организация, ДокументОбъект.Контрагент, ДокументОбъект.ДоговорКонтрагента);
		ДокументОбъект.СчетУчетаРасчетовСКонтрагентом=	СчетаРасчетов.Поставки;
		ДокументОбъект.СчетУчетаРасчетовПоАвансам= 		СчетаРасчетов.Авансы;
		
		ФормаДокумента.ТребуетсяСчетФактура= Истина;
		//ПоступлениеТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыПроСчетФактуру(ФормаДокумента);
		Если СокрЛП(ВидОперации) <> "Продажа, комиссия" Тогда
			Если ФормаДокумента.Элементы.Найти("ГруппаСчетФактураСтраницы") <> Неопределено Тогда
				ФормаДокумента.Элементы.ГруппаСчетФактураСтраницы.ТекущаяСтраница= ФормаДокумента.Элементы.ГруппаЗарегистрироватьСчетФактуру;
			Иначе
				ФормаДокумента.Элементы.СчетФактураКнопка.Видимость= Истина;
				ФормаДокумента.Элементы.СчетФактураСсылка.Видимость= Ложь;
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры	
	
	&НаСервере
	Функция ПолучитьСчетаРасчетов(Организация, Контрагент, ДоговорКонтргагента)
		СчетаРасчетов =   Вычислить("БухгалтерскийУчетПереопределяемый.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтргагента)");
		ВидДоговора = ДоговорКонтрагента.ВидДоговора;
		
		Результат = новый Структура("Поставки, Авансы");
		Если ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом")  тогда
			Результат.Поставки = СчетаРасчетов.СчетРасчетовСКомитентом;
		ИначеЕсли 	ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВПереработку")  тогда
			Результат.Поставки = ПланыСчетов.Хозрасчетный.ПустаяСсылка()
		Иначе 
			Результат.Поставки =  СчетаРасчетов.СчетРасчетов
		КонецЕсли;
		
		Результат.Авансы = СчетаРасчетов.СчетАвансов;
		
		возврат результат;
		
	КонецФункции	
	
	&НаСервере
	Функция БухгалтерскийУчетПереопределяемыйПолучитьСчетаУчетаНоменклатуры(Организация, Номенклатура, Склад);
		Возврат Вычислить("БухгалтерскийУчетПереопределяемый.ПолучитьСчетаУчетаНоменклатуры(Организация, Номенклатура, Склад)");
	КонецФункции
	
	&НаСервере
	Функция БухгалтерскийУчетПереопределяемыйСчетаУчетаОбъектовСтроительства(Организация, ОбъектСтроительства)
		Возврат Вычислить("БухгалтерскийУчетПереопределяемый.СчетаУчетаОбъектовСтроительства(Организация, ОбъектСтроительства)");
	КонецФункции
	
	&НаКлиенте
	Процедура ЗаполнитьТабличнуюЧастьДокумента(ФормаДокумента, Знач ДопДанныеССервера)

		ДокументОбъект=	ФормаДокумента.Объект;
		ТолькоУслуги= 	ТабличнаяЧасть.НайтиСтроки(Новый Структура("Тип", "Товар")).Количество()=0;
		
		Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл
			
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда 
				
				Если ВидОперации = "ПоступлениеУслугИПрочихАктивов" Тогда
					
					//документ "Поступление услуг и прочих активов"
					
					СтрОбъект=	ДокументОбъект.Расходы.Добавить();
					
					СтрОбъект.Содержание=	СтрокаТЧ.НоменклатураИзЭД;
					СтрОбъект.Количество= 	СтрокаТЧ.Количество;
					СтрОбъект.Цена= 		СтрокаТЧ.Цена;
					СтрОбъект.Сумма= 		СтрокаТЧ.Сумма; 
					СтрОбъект.СтавкаНДС= 	СтрокаТЧ.СтавкаНДС;
					СтрОбъект.СуммаНДС= 	СтрокаТЧ.СуммаНДС;
					СтрОбъект.СуммаСНДС= 	СтрокаТЧ.Всего;					
					
				Иначе
					
					СтрОбъект=	ДокументОбъект.Товары.Добавить();
				
					СтрОбъект.Номенклатура=		СтрокаТЧ.Номенклатура;
					СтрОбъект.Количество=		СтрокаТЧ.Количество;
					СтрОбъект.Цена=				СтрокаТЧ.Цена;
					Если СтрОбъект.свойство("Склад") тогда
						СтрОбъект.Склад=		ДокументОбъект.Склад;
					КонецЕсли;
					
					Если ЗаполнениеГТД И СтрокаТЧ.Тип = "Товар" Тогда
						СтрОбъект.ВедетсяУчетПоГТД=		Истина;
						СтрОбъект.ТипНоменклатуры=		ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар");
						СтрОбъект.НомерГТД= 			НайтиИлиСоздатьГТД(СтрокаТЧ.ГТД, СтрокаТЧ.СтранаПроисхождения);
						Если Найти(Объект.ПараметрыКлиентСервер.СинонимКонфигурации, "ERP")=0 Тогда
							СтрОбъект.СтранаПроисхождения= 	СтрокаТЧ.СтранаПроисхождения; 
						КонецЕсли;
					КонецЕсли;
					
					СтрОбъект.КоличествоУпаковок=	СтрокаТЧ.Количество;
					СтрОбъект.СуммаСНДС=			СтрокаТЧ.Всего;
										
				КонецЕсли;
								
			ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БГУ20" Тогда 
				
				Если ВидОперации = "ПоступлениеМЗ" Тогда
					
					СтрОбъект=	ДокументОбъект.Материалы.Добавить();
					СтрОбъект.Номенклатура=		СтрокаТЧ.Номенклатура;
					СтрОбъект.Количество=		СтрокаТЧ.Количество;
					
				ИначеЕсли ВидОперации = "ПоступлениеУслугРабот" Тогда
					
					СтрОбъект=	ДокументОбъект.УслугиИРаботы.Добавить();
					СтрОбъект.Номенклатура=		СтрокаТЧ.Номенклатура;
					СтрОбъект.Количество=		СтрокаТЧ.Количество;
					
				ИначеЕсли ВидОперации = "ПоступлениеОС" Тогда
					
					СтрОбъект=	ДокументОбъект.КапВложения.Добавить();
					СтрОбъект.ВнеоборотныйАктив= СтрокаТЧ.Номенклатура;
					СтрОбъект.Количество= 		 СтрокаТЧ.Количество;
					
				КонецЕсли;

			ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" Тогда
				
				Если ТолькоУслуги И НЕ ДокументОбъект.ВидОперации= ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя") Тогда
					СтрОбъект= ДокументОбъект.Расходы.Добавить();
				Иначе
					СтрОбъект= ДокументОбъект.Запасы.Добавить();
				КонецЕсли;
				
				СтрОбъект.Номенклатура=		СтрокаТЧ.Номенклатура;
				СтрОбъект.ЕдиницаИзмерения=	СтрокаТЧ.ЕдиницаИзмерения;
				СтрОбъект.Количество=		СтрокаТЧ.Количество;
				СтрОбъект.Цена=				СтрокаТЧ.Цена;
								
				Если ЗаполнениеГТД И СтрокаТЧ.Тип = "Товар" Тогда
					СтрОбъект.НомерГТД=				НайтиИлиСоздатьГТД(СтрокаТЧ.ГТД, СтрокаТЧ.СтранаПроисхождения);
					СтрОбъект.СтранаПроисхождения= 	СтрокаТЧ.СтранаПроисхождения; 
				КонецЕсли;
				
				СтрОбъект.Сумма=		СтрокаТЧ.Сумма;
				СтрОбъект.СуммаНДС=		СтрокаТЧ.СуммаНДС;
				СтрОбъект.СтавкаНДС=	СтрокаТЧ.СтавкаНДС;
				
				СтрОбъект.Всего= 		СтрОбъект.Сумма + ?(ДокументОбъект.СуммаВключаетНДС, 0, СтрОбъект.СуммаНДС);
							
			Иначе
				Если СокрЛП(ВидОперации) = "В переработку" Тогда
					Если СтрокаТЧ.Тип = "Товар" Тогда
						СтрОбъект=					ДокументОбъект.Товары.Добавить();
						СтрОбъект.Номенклатура=		СтрокаТЧ.Номенклатура;
						СтрОбъект.ЕдиницаИзмерения=	СтрокаТЧ.ЕдиницаИзмерения;
						СтрОбъект.Количество=		СтрокаТЧ.Количество;
						СтрОбъект.КоличествоМест=	СтрокаТЧ.Количество;
						СтрОбъект.Цена=				СтрокаТЧ.Цена;
					КонецЕсли;
				ИначеЕсли СокрЛП(ВидОперации) = "Объекты строительства" Тогда
					Если СтрокаТЧ.Тип = "Услуга" Тогда
						СтрОбъект=					ДокументОбъект.Услуги.Добавить();
						СтрОбъект.Номенклатура=		СтрокаТЧ.Номенклатура;
						СтрОбъект.Количество=		СтрокаТЧ.Количество;
						СтрОбъект.Цена=				СтрокаТЧ.Цена;
					ИначеЕсли СтрокаТЧ.Тип = "Объект стр" Тогда
						СтрОбъект=						ДокументОбъект.ОбъектыСтроительства.Добавить();
						СтрОбъект.ОбъектСтроительства=	СтрокаТЧ.Номенклатура;
					КонецЕсли;
					
				ИначеЕсли СокрЛП(ВидОперации) = "ПоступлениеДопРасходов" Тогда
					// Для документа Поступление доп. расходов табличную часть не заполняем,
					// т.к. нет известных кейсов от бизнеса
					Продолжить;
								
				Иначе
					Если СтрокаТЧ.Тип = "Оборудование" Тогда
						СтрОбъект=					ДокументОбъект.Оборудование.Добавить();
						СтрОбъект.ЕдиницаИзмерения=	СтрокаТЧ.ЕдиницаИзмерения;
					ИначеЕсли НЕ СокрЛП(ВидОперации) = "Услуги" 
						И ((СтрокаТЧ.Тип = "Товар") или (СокрЛП(ВидОперации) ="Продажа, комиссия")) Тогда
						СтрОбъект=					ДокументОбъект.Товары.Добавить();
						Если СтрОбъект.свойство("ЕдиницаИзмерения") тогда 
							СтрОбъект.ЕдиницаИзмерения=	СтрокаТЧ.ЕдиницаИзмерения;
						КонецЕсли;	
					Иначе
						СтрОбъект=				ДокументОбъект.Услуги.Добавить();
						СтрОбъект.Содержание=	СтрокаТЧ.НоменклатураИзЭД;
					КонецЕсли;
					СтрОбъект.Номенклатура=		СтрокаТЧ.Номенклатура;
					СтрОбъект.Количество=		СтрокаТЧ.Количество;
					СтрОбъект.Цена=				СтрокаТЧ.Цена;
				КонецЕсли;
				
				Если ЗаполнениеГТД И СтрокаТЧ.Тип = "Товар" Тогда
					СтрОбъект.НомерГТД= 			НайтиИлиСоздатьГТД(СтрокаТЧ.ГТД, СтрокаТЧ.СтранаПроисхождения);
					СтрОбъект.СтранаПроисхождения= 	СтрокаТЧ.СтранаПроисхождения; 
				КонецЕсли;
				
			КонецЕсли;
			
			СтрОбъект.Сумма=		СтрокаТЧ.Сумма;
			СтрОбъект.СуммаНДС=		СтрокаТЧ.СуммаНДС;
			СтрОбъект.СтавкаНДС=	СтрокаТЧ.СтавкаНДС;
			
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БГУ20" Тогда
				СтрОбъект.Всего=	СтрокаТЧ.Всего;
			ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда 
				СтрОбъект.Всего =	СтрОбъект.Сумма + ?(ДокументОбъект.СуммаВключаетНДС, 0, СтрОбъект.СуммаНДС);
			КонецЕсли;
			
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
				ЗаполнитьСчетаУчетаНоменклатуры(ДокументОбъект, СтрОбъект, СтрокаТЧ);
			ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БГУ20" Тогда
				Если ВидОперации = "ПоступлениеМЗ" Тогда
					ЗаполнитьСтрокуНоменклатуры(ДокументОбъект, СтрОбъект);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция НайтиИлиСоздатьГТД(НомерГТД, СтранаПроисхождения)
		
		Если ЗначениеЗаполнено(НомерГТД)=Ложь Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		НомерГТДСсылка = МетодСервераБезКонтекста("Модуль_ИнтеграцияУниверсальный", "ВернутьСсылкуНаГТД", НомерГТД, СтранаПроисхождения);
		
		Возврат НомерГТДСсылка;		
		
	КонецФункции	
		
	&НаКлиенте
	Процедура ЗаполнитьСтрокуНоменклатуры(ДокументОбъект, СтрокаНоменклатуры)
		
		СтруктураДанных = ПолучитьДанныеНоменклатуры(СтрокаНоменклатуры.Номенклатура, ДокументОбъект.СчетУчета);
		
		СтрокаНоменклатуры.СтавкаНДС=			СтруктураДанных.СтавкаНДС;
		СтрокаНоменклатуры.ЕдиницаИзмерения=	СтруктураДанных.ЕдиницаИзмерения;
		СтрокаНоменклатуры.СчетУчета=			СтруктураДанных.СчетУчета;
		
	КонецПроцедуры
	
	&НаСервере
	Функция ПолучитьДанныеНоменклатуры(ТекНоменклатура, ТекущийСчетУчета)
		
		СтруктураДанных = Вычислить("ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекНоменклатура, ""СтавкаНДС, ЕдиницаИзмерения, СчетУчета"")");
		Если ТекущийСчетУчета <> ПланыСчетов.ЕПСБУ.МЗ Тогда
			СтруктураДанных.СчетУчета = ТекущийСчетУчета;
		Иначе
			Если ЗначениеЗаполнено(СтруктураДанных.СчетУчета) Тогда
				СтруктураДанных.СчетУчета = Вычислить("БухгалтерскийУчетПовтИсп.ПолучитьСчетПоКоду(СтруктураДанных.СчетУчета)");
			КонецЕсли;
		КонецЕсли;
		
		Возврат СтруктураДанных;
		
	КонецФункции
	
	&НаСервере
	Функция ПолучитьДоговорКонтрагентаВидДоговора(ДоговорКонтрагента)
		
		Возврат Вычислить("ДоговорКонтрагента.ВидДоговора");
		
	КонецФункции
	
	&НаКлиенте
	Процедура ЗаполнитьСчетаУчетаНоменклатуры(ДокОбъект, СтрокаТаблицы, СтрЭД)
		
		Если СтрЭД.Тип = "Объект стр" тогда
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ОбъектСтроительства) Тогда
				Возврат;
			КонецЕсли;
			
			СчетаУчета=	БухгалтерскийУчетПереопределяемыйСчетаУчетаОбъектовСтроительства(ДокОбъект.Организация, СтрокаТаблицы.ОбъектСтроительства)
		Иначе
			СчетаУчета=	БухгалтерскийУчетПереопределяемыйПолучитьСчетаУчетаНоменклатуры(ДокОбъект.Организация, СтрокаТаблицы.Номенклатура, ДокОбъект.Склад);
		КонецЕсли;
		
		Если СтрЭД.Тип = "Оборудование" Тогда
			СтрокаТаблицы.СчетУчета    = СчетаУчета.СчетУчета;
			СтрокаТаблицы.СчетУчетаНДС = СчетаУчета.СчетУчетаНДС;
			СтрокаТаблицы.СпособУчетаНДС = СчетаУчета.СпособУчетаНДС;
		ИначеЕсли СтрЭД.Тип = "ВозвратнаяТара" Тогда
			СтрокаТаблицы.СчетУчета    = СчетаУчета.СчетУчета;
		ИначеЕсли (СтрЭД.Тип = "Услуга") и (СокрЛП(ВидОперации)<>"Продажа, комиссия") Тогда
			СтрокаТаблицы.СчетЗатрат   = СчетаУчета.СчетУчета;
			СтрокаТаблицы.СчетУчетаНДС = СчетаУчета.СчетУчетаНДС;
			СтрокаТаблицы.СпособУчетаНДС = СчетаУчета.СпособУчетаНДС;
			
			СтрокаТаблицы.Субконто1    = СчетаУчета.Субконто1;
			СтрокаТаблицы.Субконто2    = СчетаУчета.Субконто2;
			СтрокаТаблицы.Субконто3    = СчетаУчета.Субконто3;
			
			СтрокаТаблицы.СчетЗатратНУ = СчетаУчета.СчетУчета;
			СтрокаТаблицы.СубконтоНУ1  = СчетаУчета.Субконто1;
			СтрокаТаблицы.СубконтоНУ2  = СчетаУчета.Субконто2;
			СтрокаТаблицы.СубконтоНУ3  = СчетаУчета.Субконто3;
		ИначеЕсли СтрЭД.Тип = "Объект стр" Тогда
			СтрокаТаблицы.СчетУчета    = СчетаУчета.СчетУчета;
			СтрокаТаблицы.СчетУчетаНДС = СчетаУчета.СчетУчетаНДС;
			СтрокаТаблицы.СпособУчетаНДС = СчетаУчета.СпособУчетаНДС;
		Иначе
			Если ДокОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия") Тогда
				Если Вычислить("ПолучитьДоговорКонтрагентаВидДоговора(ДокОбъект.ДоговорКонтрагента)") = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом")
					И СчетаУчета.СчетУчетаЯвляетсяЗабалансовым <> Истина Тогда
					СтрокаТаблицы.СчетУчета = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПустаяСсылка");
				Иначе
					СтрокаТаблицы.СчетУчета = СчетаУчета.СчетУчета;
					СтрокаТаблицы.СпособУчетаНДС = СчетаУчета.СпособУчетаНДС;
				КонецЕсли;
				СтрокаТаблицы.СчетУчетаНДС = СчетаУчета.СчетУчетаНДС;
			ИначеЕсли ДокОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку") Тогда
				СтрокаТаблицы.СчетУчета = СчетаУчета.СчетУчетаДавСырья;
			Иначе
				Если СчетаУчета.СчетУчетаЯвляетсяЗабалансовым <> Истина Тогда
					СтрокаТаблицы.СчетУчета = СчетаУчета.СчетУчета;
					СтрокаТаблицы.СпособУчетаНДС = СчетаУчета.СпособУчетаНДС;
				Иначе
					СтрокаТаблицы.СчетУчета = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПустаяСсылка");
				КонецЕсли;
				если строкаТаблицы.Свойство("СчетУчетаНДС") тогда 
					СтрокаТаблицы.СчетУчетаНДС = СчетаУчета.СчетУчетаНДС;
					СтрокаТаблицы.СпособУчетаНДС = СчетаУчета.СпособУчетаНДС;
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПроверитьВсеНеобходимыеДанные()
		
		ВсеОК= Истина;
		
		Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
			Сообщить("Не указан контрагент");
			ВсеОК= Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			Сообщить("Не заполнена организация");
			ВсеОК= Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ВидОперации) Тогда
			Сообщить("Не указан вид операции");
			ВсеОК= Ложь;
		КонецЕсли;
		
		Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл
			
			НомерСтрокиТЧ = СтрокаТЧ.НомерСтроки + 1;
			
			Если СтрокаТЧ.Тип <> "Услуга" И НЕ ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
				Сообщить("В строке " + НомерСтрокиТЧ + " не указана номенклатура");
				ВсеОК= Ложь;
			КонецЕсли;
			
			Если ИспользоватьЕдиницуИзмерения Тогда
				Если НЕ ЗначениеЗаполнено(СтрокаТЧ.ЕдиницаИзмерения) И СтрокаТЧ.Тип <> "Услуга" И СтрокаТЧ.Тип <> "Основное средство" Тогда
					Сообщить("В строке " + НомерСтрокиТЧ + " не указана единица измерения");
					ВсеОК= Ложь;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗаполнениеГТД = Истина Тогда
				
				Если ЗначениеЗаполнено(СтрокаТЧ.ГТД) = Истина
					И ЗначениеЗаполнено(СтрокаТЧ.СтранаПроисхождения) = Ложь Тогда
					
					Сообщить("В строке " + НомерСтрокиТЧ + " не указана страна происходжения товара");
					ВсеОК= Ложь;
					
				ИначеЕсли ЗначениеЗаполнено(СтрокаТЧ.ГТД) = Ложь
					И ЗначениеЗаполнено(СтрокаТЧ.СтранаПроисхождения) = Истина Тогда
					
					Сообщить("В строке " + НомерСтрокиТЧ + " не указан номер ГТД");
					ВсеОК= Ложь;
				КонецЕсли;
				
			КонецЕсли;	
			
		КонецЦикла;
		
		Если (СокрЛп(ВидОперации) =  "В переработку") И (ТабличнаяЧасть.НайтиСтроки(Новый Структура("Тип", "Услуга")).Количество() > 0 ) Тогда
			Сообщить("Невозможно создание документа ""Возврат товаров от покупателя"", содержащего строки с услугами.");
			ВсеОК= Ложь;
		КонецЕсли;
		
		Возврат ВсеОК;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ПерейтиКСозданиюДокумента1С()

		УстановитьСоответствиеНоменклатуры();
		УстановитьСоответствиеДоговора(ДоговорКонтрагента, ДоговорСвойство);
		
		ФормаДокумента= ПолучитьФормуСоздаваемогоДокумента();
		ДокументОбъект=	ФормаДокумента.Объект;
		
		ДопДанныеССервера= ПодготовитьДопДанныеДляСозданияНакладной();
		
		ЗаполнитьШапкуДокумента(ДокументОбъект, ДопДанныеССервера);
		УправлениеФормойДокумента(ДокументОбъект, ФормаДокумента, ДопДанныеССервера);
		ЗаполнитьТабличнуюЧастьДокумента(ФормаДокумента, ДопДанныеССервера);
		
		ЗаполнитьИтогиПоТабличнымЧастям(ДокументОбъект, ФормаДокумента);
				
		//заполним счета учета для БП 30
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
			УстановитьСчетаРасчетовСКонтргаентом(ФормаДокумента);
		КонецЕсли;
		
		ФормаДокумента.Модифицированность=	Истина;
		ФормаДокумента.РежимОткрытияОкна=	РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
		
		ФормаДокумента.Открыть();
		
		ПодключитьОбработчикОжидания("ОбработчикЗакрытиеФормыНакладной", 0.1, Истина);
				
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПолучитьФормуСоздаваемогоДокумента()
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
			
			СтруктураПараметров= Новый Структура("ЗначенияЗаполнения", Новый Структура);
			СтруктураПараметров.ЗначенияЗаполнения.Вставить("ВидОперации"		, ВидОперации);
			СтруктураПараметров.ЗначенияЗаполнения.Вставить("Организация"		, Организация);
			СтруктураПараметров.ЗначенияЗаполнения.Вставить("ДоговорКонтрагента", ДоговорКонтрагента);
			
			Если СокрЛП(ВидОперации) = "Продажа, комиссия" Тогда
				ВидДокумента= "ВозвратТоваровОтПокупателя";
			ИначеЕсли СокрЛП(ВидОперации) = "ПоступлениеДопРасходов" Тогда
				ВидДокумента= "ПоступлениеДопРасходов"; 
			Иначе
				ВидДокумента= "ПоступлениеТоваровУслуг";
			КонецЕсли;
			
			ФормаДокумента= ПолучитьФорму("Документ." + ВидДокумента + ".ФормаОбъекта", СтруктураПараметров);	
			
		ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
			
			Если СокрЛП(ВидОперации) = "Возврат товаров от клиента" Тогда 
				ФормаДокумента=	ПолучитьФорму("Документ.ВозвратТоваровОтКлиента.ФормаОбъекта");
			ИначеЕсли СокрЛП(ВидОперации) = "ПоступлениеУслугИПрочихАктивов" Тогда
				ФормаДокумента=	ПолучитьФорму("Документ.ПоступлениеУслугПрочихАктивов.ФормаОбъекта");
			Иначе 	
				ФормаДокумента=	ПолучитьФорму("Документ.ПоступлениеТоваровУслуг.ФормаОбъекта");
			КонецЕсли;	
			
		ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БГУ20" Тогда
			ФормаДокумента= ПолучитьФорму("Документ."+ВидОперации+".ФормаОбъекта");
		ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" Тогда
			ФормаДокумента=	ПолучитьФорму("Документ.ПриходнаяНакладная.Форма.ФормаДокумента");
		КонецЕсли;
		
		Возврат ФормаДокумента; 
		
	КонецФункции
	
	&НаКлиенте
	// Для минимизации вызовов сервера, соберем разом доп. данные
	Функция ПодготовитьДопДанныеДляСозданияНакладной()
		
		ДопДанныеССервера= 	Новый Структура();
		МаркерКонфигурации= Объект.ПараметрыКлиентСервер.МаркерКонфигурации;
		
		СтруктураВходящиеДанные= Новый Структура();
		СтруктураВходящиеДанные.Вставить("ДоговорКонтрагента", ДоговорКонтрагента);
		
		Если МаркерКонфигурации = "УНФ16" Тогда
			ИмяМодуляИнтеграции= 	Объект.ПараметрыКлиентСервер.ИмяФормыИнтеграции; 
			ДопДанныеССервера=		МетодСервераБезКонтекста(ИмяМодуляИнтеграции, "ПодготовитьДопДанныеДляСозданияНакладной", СтруктураВходящиеДанные);
		КонецЕсли;
		
		Возврат ДопДанныеССервера;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ЗаполнитьШапкуДокумента(ДокументОбъект, Знач ДопДанныеССервера)
		
		ДокументОбъект.Организация=	Организация;
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" И СокрЛП(ВидОперации) = "ПоступлениеДопРасходов" Тогда
			ЗаполнитьШапкуДокумента_БП30_ПоступлениеДопРасходов(ДокументОбъект);
		ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" И СокрЛП(ВидОперации) = "ПоступлениеУслугИПрочихАктивов" Тогда
			ЗаполнитьШапкуДокумента_УТ11_ПоступлениеУслугИПрочихАктивов(ДокументОбъект);
		Иначе
			ЗаполнитьШапкуДокумента_Универсальный(ДокументОбъект, ДопДанныеССервера);
		КонецЕсли;
			
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьШапкуДокумента_БП30_ПоступлениеДопРасходов(ДокументОбъект)
		
		ДокументОбъект.Дата= ТекущаяДата();
		
		ДокументОбъект.ДатаВходящегоДокумента= 	ЭДОбъект.DocumentDate;
		ДокументОбъект.НомерВходящегоДокумента= ЭДОбъект.DocumentNumber; 
		
		ДокументОбъект.Контрагент=				Контрагент;
		ДокументОбъект.СуммаВключаетНДС= 		СуммаВключаетНДС;
		ДокументОбъект.СтавкаНДС= 		 		Неопределено; //принудительно сбрасываем ставку, т.к. по умолчанию ставится 18%
		ДокументОбъект.СпособРаспределения= 	ПредопределенноеЗначение("Перечисление.СпособыРаспределенияДопРасходов.ПоСумме");
				
		ДокументОбъект.Сумма= 					ЭДОбъект.Total;
		ДокументОбъект.СуммаНДС= 				ЭДОбъект.Vat; 
							
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьШапкуДокумента_УТ11_ПоступлениеУслугИПрочихАктивов(ДокументОбъект)
		
		ДокументОбъект.Дата= ТекущаяДата();
		
		ДокументОбъект.ДатаВходящегоДокумента= 	ЭДОбъект.DocumentDate;
		ДокументОбъект.НомерВходящегоДокумента= ЭДОбъект.DocumentNumber; 
		
		Партнер= ПолучитьПартнера(Контрагент);
				
		Если ЗначениеЗаполнено(Партнер) Тогда
			ДокументОбъект.Партнер=		Партнер;
			ДокументОбъект.Контрагент=	Контрагент;
		КонецЕсли;
		
		ДокументОбъект.ЦенаВключаетНДС=	СуммаВключаетНДС;
		ДокументОбъект.ПорядокРасчетов=	ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоНакладным");
		
		ДокументОбъект.Соглашение= Соглашение;
				
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			
			ДокументОбъект.Договор=	ДоговорКонтрагента;
			
			ДанныеДоговора=	ПолучитьДанныеПоДоговору(ДоговорКонтрагента);
			
			Если ДокументОбъект.БанковскийСчетОрганизации <> Неопределено И ЗначениеЗаполнено(ДанныеДоговора.БанковскийСчет) Тогда
				ДокументОбъект.БанковскийСчетОрганизации= ДанныеДоговора.БанковскийСчет;
			КонецЕсли;
			
			Если ДокументОбъект.БанковскийСчетКонтрагента <> Неопределено И ЗначениеЗаполнено(ДанныеДоговора.БанковскийСчетКонтрагента) Тогда
				ДокументОбъект.БанковскийСчетКонтрагента= ДанныеДоговора.БанковскийСчетКонтрагента;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьШапкуДокумента_Универсальный(ДокументОбъект, Знач ДопДанныеССервера)
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БГУ20" Тогда
			ДокументОбъект.ДатаПервичногоДокумента=		ЭДОбъект.DocumentDate;
			ДокументОбъект.НомерПервичногоДокумента=	ЭДОбъект.DocumentNumber;
		ИначеЕсли НЕ СокрЛП(ВидОперации) = "Продажа, комиссия" Тогда //если не возврат
			ДокументОбъект.ДатаВходящегоДокумента=		ЭДОбъект.DocumentDate;
			ДокументОбъект.НомерВходящегоДокумента=		ЭДОбъект.DocumentNumber;
		Иначе 
			ДокументОбъект.ВидОперации= ПредопределенноеЗначение("Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.ПродажаКомиссия");
			ДокументОбъект.ПокупателемВыставляетсяСчетФактураНаВозврат= ложь;
		КонецЕсли;
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
			ДокументОбъект.ЦенаВключаетНДС=	 СуммаВключаетНДС;
		Иначе
			ДокументОбъект.СуммаВключаетНДС= СуммаВключаетНДС;
		КонецЕсли;
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
			
			Партнер= ПолучитьПартнера(Контрагент);
			
			Если ЗначениеЗаполнено(Партнер) Тогда
				ДокументОбъект.Партнер=		Партнер;
				ДокументОбъект.Контрагент=	Контрагент;
			КонецЕсли;
			
			ДокументОбъект.ХозяйственнаяОперация= ВидОперации;
			
			ДокументОбъект.Соглашение= Соглашение;
			Если ЗначениеЗаполнено(Соглашение) И МетодКлиента("Модуль_Клиент", "ПолучитьЗначениеРеквизитаОбъекта", Соглашение, "ИспользуютсяДоговорыКонтрагентов") = Истина Тогда
				ДокументОбъект.Договор=	ДоговорКонтрагента;
				ФормаДокумента.Элементы.Договор.Доступность= Истина;
			Иначе
				ФормаДокумента.Элементы.Договор.Доступность= Ложь;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
				
				ДокументОбъект.Договор=	ДоговорКонтрагента;
				
				ДанныеДоговора=	ПолучитьДанныеПоДоговору(ДоговорКонтрагента);
				
				Если ДокументОбъект.БанковскийСчетОрганизации <> Неопределено И ЗначениеЗаполнено(ДанныеДоговора.БанковскийСчет) Тогда
					ДокументОбъект.БанковскийСчетОрганизации= ДанныеДоговора.БанковскийСчет;
				КонецЕсли;
				
				Если ДокументОбъект.БанковскийСчетКонтрагента <> Неопределено И ЗначениеЗаполнено(ДанныеДоговора.БанковскийСчетКонтрагента) Тогда
					ДокументОбъект.БанковскийСчетКонтрагента= ДанныеДоговора.БанковскийСчетКонтрагента;
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			Если НЕ Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БГУ20" Тогда
				ДокументОбъект.ВидОперации=	ВидОперации;
			КонецЕсли;
			
			ДокументОбъект.Контрагент=	Контрагент;
			
			Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
				Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БГУ20" Тогда
					ДокументОбъект.Договор=				ДоговорКонтрагента;
				ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" Тогда
					ДокументОбъект.Договор=				ДоговорКонтрагента;
				Иначе
					ДокументОбъект.ДоговорКонтрагента=	ДоговорКонтрагента;
				КонецЕсли;
			КонецЕсли;
						
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
				ДокументОбъект.КратностьВзаиморасчетов=	1;
			ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" Тогда
				ДокументОбъект.Кратность= 1;
 			КонецЕсли;
			
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" Тогда
				ТипЦен= ДопДанныеССервера.ТипЦен;
			Иначе
				ТипЦен= МетодСервераБезКонтекста("Модуль_ИнтеграцияУниверсальный", "ПолучитьТипЦенПоДоговору", ДоговорКонтрагента);
			КонецЕсли;
			Если НЕ ТипЦен = Неопределено Тогда
				ДокументОбъект.ТипЦен= ТипЦен;
			КонецЕсли;
								
		КонецЕсли;
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БГУ20" Тогда
			
			Если ВидОперации = "ПоступлениеМЗ" Тогда
				
				ДокументОбъект.СчетУчета= ПредопределенноеЗначение("ПланСчетов.ЕПСБУ.МЗ");
				
				ДанныеСчетовУчетаМЗ= ПолучитьДанныеСчетовУчетаМЗ(ДокументОбъект.Организация);
				Для каждого СчетУчетаБГУ Из ДанныеСчетовУчетаМЗ.СписокСчетовУчета Цикл
					ФормаДокумента.Элементы.СчетУчета.СписокВыбора.Добавить(СчетУчетаБГУ.Значение, СчетУчетаБГУ.Представление);
				КонецЦикла;
				
				ФормаДокумента.ДополнительныеСубконтоСчетов= Новый ФиксированноеСоответствие(ДанныеСчетовУчетаМЗ.ВидыСубконтоСчетов);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
		
	&НаКлиенте 
	Процедура УправлениеФормойДокумента(ДокументОбъект, ФормаДокумента, Знач ДопДанныеССервера)
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" Тогда
			УправлениеФормойДокумента_УНФ(ДокументОбъект, ФормаДокумента, ДопДанныеССервера);
		ИначеЕсли НЕ Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
			Если ВидОперации = "ПоступлениеДопРасходов" Тогда
				Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
					УправлениеФормойДокумента_БП30_ПоступлениеДопРасходов(ДокументОбъект, ФормаДокумента);
				КонецЕсли;
			Иначе	
				УправлениеФормойДокумента_Универсальный(ДокументОбъект, ФормаДокумента, ДопДанныеССервера);
			КонецЕсли;
		КонецЕсли;
					
	КонецПроцедуры
	
	&НаКлиенте
	Процедура УправлениеФормойДокумента_БП30_ПоступлениеДопРасходов(ДокументОбъект, ФормаДокумента)
		
		МаркерКонфигурации= Объект.ПараметрыКлиентСервер.МаркерКонфигурации;
		
		Если НЕ ФормаДокумента.Элементы.Найти("ЦеныИВалюта") = Неопределено Тогда
									
			СтруктураНадписи= Новый Структура("ВалютаДокумента, ТипЦен, Курс, Кратность, СуммаВключаетНДС");
									
			СтруктураНадписи.ВалютаДокумента= 	ДокументОбъект.ВалютаДокумента;
			СтруктураНадписи.Курс= 				ДокументОбъект.КурсВзаиморасчетов;
			СтруктураНадписи.Кратность= 		ДокументОбъект.КратностьВзаиморасчетов;
			СтруктураНадписи.СуммаВключаетНДС= 	ДокументОбъект.СуммаВключаетНДС;
											
			ФормаДокумента.ЦеныИВалюта= Вычислить("ОбщегоНазначенияБПКлиентСервер.СформироватьНадписьЦеныИВалюта(СтруктураНадписи)");
									
		КонецЕсли;
		
	КонецПроцедуры
		
	&НаКлиенте
	Процедура УправлениеФормойДокумента_Универсальный(ДокументОбъект, ФормаДокумента, Знач ДопДанныеССервера)
		
		МаркерКонфигурации= Объект.ПараметрыКлиентСервер.МаркерКонфигурации;
		
		Если МаркерКонфигурации = "БГУ20" Тогда
			ДоговорУказан= ЗначениеЗаполнено(ДокументОбъект.Договор);
		Иначе
			ДоговорУказан= ЗначениеЗаполнено(ДокументОбъект.ДоговорКонтрагента);
		КонецЕсли;
		
		ЭтоКомиссия= Ложь;
		Если МаркерКонфигурации = "БП30" Тогда
			ЭтоКомиссия= ДоговорУказан И Вычислить("ПолучитьДоговорКонтрагентаВидДоговора(ДокументОбъект.ДоговорКонтрагента) = ПредопределенноеЗначение(""Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"")");
		КонецЕсли;
		
		//Управление формой для БП
		Если НЕ ФормаДокумента.Элементы.Найти("ГруппаТовары") = Неопределено Тогда
			ФормаДокумента.Элементы.ГруппаТовары.Видимость=
				ДокументОбъект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства");
		КонецЕсли;
		
		Если НЕ МаркерКонфигурации = "БГУ20" Тогда
			Если НЕ ФормаДокумента.Элементы.Найти("ГруппаВозвратнаяТара") = Неопределено Тогда
				ФормаДокумента.Элементы.ГруппаВозвратнаяТара.Видимость=
					ДокументОбъект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку")
					И ДокументОбъект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства");
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ФормаДокумента.Элементы.Найти("ГруппаУслуги") = Неопределено Тогда
			ФормаДокумента.Элементы.ГруппаУслуги.Видимость=
				(ДокументОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия")
				ИЛИ ДокументОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Оборудование")
				ИЛИ ДокументОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства"))
				И НЕ ФормаДокумента.ЭтоКомиссия;
		КонецЕсли;
		
		Если НЕ ФормаДокумента.Элементы.Найти("ГруппаАгентскиеУслуги") = Неопределено Тогда
			ФормаДокумента.Элементы.ГруппаАгентскиеУслуги.Видимость= 
				НЕ ЭтоКомиссия И ДокументОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия");
		КонецЕсли;
			
		Если НЕ ФормаДокумента.Элементы.Найти("ГруппаОборудование") = Неопределено Тогда
			ФормаДокумента.Элементы.ГруппаОборудование.Видимость=
				ДокументОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Оборудование");
		КонецЕсли;	
		
		Если НЕ ФормаДокумента.Элементы.Найти("ГруппаОбъектыСтроительства") = Неопределено Тогда
			ФормаДокумента.Элементы.ГруппаОбъектыСтроительства.Видимость=
				ДокументОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства");
		КонецЕсли;
		
		Если НЕ ФормаДокумента.Элементы.Найти("ГруппаСчетФактура") = Неопределено Тогда
			ФормаДокумента.Элементы.ГруппаСчетФактура.Видимость= 
				НЕ (ФормаДокумента.Элементы.Найти("УчетАгентскогоНДС") <> Неопределено И ФормаДокумента.УчетАгентскогоНДС) И НЕ ЭтоКомиссия
				И ДокументОбъект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку");
		КонецЕсли;
		
		Если НЕ ФормаДокумента.Элементы.Найти("ГруппаСчетаРасчетов") = Неопределено Тогда
			ФормаДокумента.Элементы.ГруппаСчетаРасчетов.Видимость=
				ДокументОбъект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку");
		КонецЕсли;
		
		Если НЕ ФормаДокумента.Элементы.Найти("ГруппаИтогиВсегоНДС") = Неопределено Тогда
			ФормаДокумента.Элементы.ГруппаИтогиВсегоНДС.Доступность=
				ДокументОбъект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку");
		КонецЕсли;
		
		ЗачетПоДокументу= 	Ложь;
		ЗачетАвтоматически= Ложь;
		
		Если МаркерКонфигурации = "БП30" Тогда
			
			ЗачетАвансовВозможен= 
				НЕ ЭтоКомиссия И ДокументОбъект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку");
			
			Если НЕ ФормаДокумента.Элементы.Найти("ГруппаЗачетАвансов") = Неопределено Тогда
				Если ЗачетАвансовВозможен Тогда
					Если ДокументОбъект.СпособЗачетаАвансов = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.ПоДокументу") Тогда
						ЗачетПоДокументу= Истина;
					ИначеЕсли ДокументОбъект.СпособЗачетаАвансов = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.Автоматически") Тогда
						ЗачетАвтоматически= Истина;
					КонецЕсли;
				КонецЕсли;
				
				ФормаДокумента.Элементы.ГруппаЗачетАвансов.Видимость= ЗачетПоДокументу;
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ФормаДокумента.Элементы.Найти("СчетУчетаРасчетовПоАвансам") = Неопределено Тогда
			ФормаДокумента.Элементы.СчетУчетаРасчетовПоАвансам.Доступность= ЗачетПоДокументу ИЛИ ЗачетАвтоматически;
		КонецЕсли;
			
		Если НЕ ФормаДокумента.Элементы.Найти("НадписьСчетФактура") = Неопределено Тогда
			ФормаДокумента.Элементы.НадписьСчетФактура.Гиперссылка= ФормаДокумента.ТребуетсяСчетФактура;
		КонецЕсли;
			
		Если НЕ ФормаДокумента.Элементы.Найти("НомерВходящегоСчетаФактуры") = Неопределено Тогда
			ФормаДокумента.Элементы.НомерВходящегоСчетаФактуры.Доступность= ДокументОбъект.ПредъявленСчетФактура;
		КонецЕсли;
			
		Если НЕ ФормаДокумента.Элементы.Найти("ДатаВходящегоСчетаФактуры") = Неопределено Тогда
			ФормаДокумента.Элементы.ДатаВходящегоСчетаФактуры.Доступность= ДокументОбъект.ПредъявленСчетФактура;
		КонецЕсли;
			
		Если НЕ ФормаДокумента.Элементы.Найти("КодВидаОперации") = Неопределено Тогда
			ФормаДокумента.Элементы.КодВидаОперации.Доступность= ДокументОбъект.ПредъявленСчетФактура;
		КонецЕсли;
			
		Если НЕ ФормаДокумента.Элементы.Найти("НДСПредъявленКВычету") = Неопределено Тогда
			
			ВерсияУчетаНДС= Вычислить("УчетНДСКлиентСервер.Версия(ДокументОбъект.Дата)");
			
			ФормаДокумента.Элементы.НДСПредъявленКВычету.Доступность=
				ДокументОбъект.ПредъявленСчетФактура И НЕ ДокументОбъект.НДСВключенВСтоимость И (ФормаДокумента.УпрощенныйУчетНДС ИЛИ ВерсияУчетаНДС = 2);
		КонецЕсли;
			
		Если НЕ ФормаДокумента.Элементы.Найти("Склад") = Неопределено
			И (МаркерКонфигурации = "БГУ20" ИЛИ МаркерКонфигурации = "БП30") Тогда
			ФормаДокумента.Элементы.Склад.Доступность =
				ДокументОбъект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства");
		КонецЕсли;
		
		Если НЕ ФормаДокумента.Элементы.Найти("ЦеныИВалюта") = Неопределено Тогда
									
			Если МаркерКонфигурации = "БП30" ИЛИ МаркерКонфигурации = "БГУ20" Тогда
				
				СтруктураНадписи= Новый Структура("ВалютаДокумента, ТипЦен, Курс, Кратность");
										
				СтруктураНадписи.ВалютаДокумента= 	ДокументОбъект.ВалютаДокумента;
				СтруктураНадписи.ТипЦен= 			ДокументОбъект.ТипЦен;
				СтруктураНадписи.Курс= 				ДокументОбъект.КурсВзаиморасчетов;
				СтруктураНадписи.Кратность= 		ДокументОбъект.КратностьВзаиморасчетов;
				
				Если ДокументОбъект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку") Тогда
					СтруктураНадписи.Вставить("СуммаВключаетНДС", ДокументОбъект.СуммаВключаетНДС);
				КонецЕсли;
							
				ФормаДокумента.ЦеныИВалюта= Вычислить("ОбщегоНазначенияБПКлиентСервер.СформироватьНадписьЦеныИВалюта(СтруктураНадписи)");
						
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ФормаДокумента.Элементы.Найти("ДоговорКонтрагента") = Неопределено Тогда
			ФормаДокумента.Элементы.ДоговорКонтрагента.Доступность= 
				ЗначениеЗаполнено(ДокументОбъект.Организация) И ЗначениеЗаполнено(ДокументОбъект.Контрагент);
		КонецЕсли;	
		
		Если НЕ ФормаДокумента.Элементы.Найти("ПодразделениеОрганизации") = Неопределено Тогда
			ФормаДокумента.Элементы.ПодразделениеОрганизации.Доступность= ЗначениеЗаполнено(ДокументОбъект.Организация);
		КонецЕсли;
		
		Если НЕ ФормаДокумента.Элементы.Найти("СпособЗачетаАвансов") = Неопределено Тогда
			ФормаДокумента.Элементы.СпособЗачетаАвансов.Доступность= ЗначениеЗаполнено(ДокументОбъект.ДоговорКонтрагента) И ЗачетАвансовВозможен;
		КонецЕсли;
		
		Если НЕ ФормаДокумента.Элементы.Найти("ТоварыКонтрагент") = Неопределено Тогда
			ФормаДокумента.Элементы.ТоварыКонтрагент.Доступность= НЕ ЭтоКомиссия;
		КонецЕсли;
		
		Если НЕ ФормаДокумента.Элементы.Найти("ТоварыДоговорКонтрагента") = Неопределено Тогда
			ФормаДокумента.Элементы.ТоварыДоговорКонтрагента.Доступность= НЕ ЭтоКомиссия;
		КонецЕсли;
		
		Если НЕ ФормаДокумента.Элементы.Найти("ТоварыСчетРасчетов") = Неопределено Тогда
			ФормаДокумента.Элементы.ТоварыСчетРасчетов.Доступность= НЕ ЭтоКомиссия;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура УправлениеФормойДокумента_УНФ(ДокументОбъект, ФормаДокумента, ДопДанныеССервера);
		
		Если НЕ ФормаДокумента.Элементы.Найти("ГруппаРасходы") = Неопределено Тогда
			ФормаДокумента.Элементы.ГруппаРасходы.Видимость=
				ДокументОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика");
		КонецЕсли;
								
		СтруктураНадписи= Новый Структура("ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, ВидЦенКонтрагента, НалогообложениеНДС");
				
		СтруктураНадписи.ВалютаДокумента= 			ДокументОбъект.ВалютаДокумента;
		СтруктураНадписи.ВалютаРасчетов= 			ДопДанныеССервера.ВалютаРасчетов;
		СтруктураНадписи.Курс= 						ДокументОбъект.Курс;
		СтруктураНадписи.КурсНациональнаяВалюта= 	ДокументОбъект.Курс;
		СтруктураНадписи.СуммаВключаетНДС= 			ДокументОбъект.СуммаВключаетНДС;
		СтруктураНадписи.УчетВалютныхОпераций= 		ДопДанныеССервера.УчетВалютныхОпераций;
		СтруктураНадписи.ВидЦенКонтрагента= 		ДокументОбъект.ВидЦенКонтрагента;
		СтруктураНадписи.НалогообложениеНДС= 		ДокументОбъект.НалогообложениеНДС;
		
		ФормаДокумента.ЦеныИВалюта= СформироватьНадписьЦеныИВалюта_УНФ(СтруктураНадписи);
				
	КонецПроцедуры
	
	&НаКлиенте
	Функция СформироватьНадписьЦеныИВалюта_УНФ(СтруктураНадписи)
	
		ТекстНадписи = "";
		
		// Валюта.
		Если СтруктураНадписи.УчетВалютныхОпераций Тогда
			Если ЗначениеЗаполнено(СтруктураНадписи.ВалютаДокумента) Тогда
				ТекстНадписи= НСтр("ru = '%Валюта%'");
				ТекстНадписи= СтрЗаменить(ТекстНадписи, "%Валюта%", СокрЛП(Строка(СтруктураНадписи.ВалютаДокумента)));
			КонецЕсли;
		КонецЕсли;
			
		// Вид цен контрагента.
		Если ЗначениеЗаполнено(СтруктураНадписи.ВидЦенКонтрагента) Тогда
			Если ПустаяСтрока(ТекстНадписи) Тогда
				ТекстНадписи= ТекстНадписи + НСтр("ru = '%ВидЦенКонтрагента%'");
			Иначе	
				ТекстНадписи= ТекстНадписи + НСтр("ru = ' • %ВидЦенКонтрагента%'");
			КонецЕсли;	
			ТекстНадписи= СтрЗаменить(ТекстНадписи, "%ВидЦенКонтрагента%", СокрЛП(Строка(СтруктураНадписи.ВидЦенКонтрагента)));
		КонецЕсли;
		
		// Налогообложение НДС.
		Если ЗначениеЗаполнено(СтруктураНадписи.НалогообложениеНДС) Тогда
			Если ПустаяСтрока(ТекстНадписи) Тогда
				ТекстНадписи= ТекстНадписи + НСтр("ru = '%НалогообложениеНДС%'");
			Иначе
				ТекстНадписи= ТекстНадписи + НСтр("ru = ' • %НалогообложениеНДС%'");
			КонецЕсли;	
			ПодстрокаЗамены= "";
			Попытка
				ПодстрокаЗамены= Вычислить("РаботаСФормойДокументаКлиентСервер.КраткоеПредставлениеТипаНалогообложенияНДС(СтруктураНадписи.НалогообложениеНДС)");
			Исключение
				ПодстрокаЗамены= СокрЛП(Строка(СтруктураНадписи.НалогообложениеНДС));
			КонецПопытки;
			
			ТекстНадписи= СтрЗаменить(ТекстНадписи, "%НалогообложениеНДС%", ПодстрокаЗамены);
		КонецЕсли;
		
		// Флаг сумма включает НДС.
		Если ПустаяСтрока(ТекстНадписи) Тогда	
			Если СтруктураНадписи.СуммаВключаетНДС Тогда	
				ТекстНадписи= НСтр("ru = 'Сумма включает НДС'");
			Иначе		
				ТекстНадписи= НСтр("ru = 'Сумма не включает НДС'");
			КонецЕсли;	
		КонецЕсли;	
	 
		Возврат ТекстНадписи;
	
	КонецФункции

	&НаКлиенте
	Процедура ЗаполнитьИтогиПоТабличнымЧастям(ДокументОбъект, ФормаДокумента)
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
			
			Если ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия") Тогда
				
				МассивТабличныхЧастейДляРасчетаИтогов= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("Товары,Услуги,АгентскиеУслуги", ",");
			
			ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Услуги") Тогда
				
				МассивТабличныхЧастейДляРасчетаИтогов= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("Услуги", ",");
			
			ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку") Тогда
				
				МассивТабличныхЧастейДляРасчетаИтогов= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("Товары", ",");
				
			ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Оборудование") Тогда
				
				МассивТабличныхЧастейДляРасчетаИтогов= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("Оборудование,Товары,Услуги", ",");
				
			ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства") Тогда
				
				МассивТабличныхЧастейДляРасчетаИтогов= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("ОбъектыСтроительства,Услуги", ",");
				
			ИначеЕсли СокрЛП(ВидОперации) = "Продажа, комиссия" тогда 
				
				МассивТабличныхЧастейДляРасчетаИтогов= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("Товары", ",");
			ИначеЕсли СокрЛП(ВидОперации) = "ПоступлениеДопРасходов" Тогда  
				
				МассивТабличныхЧастейДляРасчетаИтогов= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("Товары", ",");
			Иначе
				МассивТабличныхЧастейДляРасчетаИтогов= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("Оборудование,ОбъектыСтроительства,Товары,Услуги,АгентскиеУслуги", ",");
			КонецЕсли;
			
			СуммаИтогов=	0;
			СуммаНДСИтогов=	0;
			
			Для Каждого ТекущаяТабличнаяЧасть Из МассивТабличныхЧастейДляРасчетаИтогов Цикл
				СуммаИтогов=	СуммаИтогов 	+ ФормаДокумента.Объект[ТекущаяТабличнаяЧасть].Итог("Сумма");
				СуммаНДСИтогов=	СуммаНДСИтогов 	+ ФормаДокумента.Объект[ТекущаяТабличнаяЧасть].Итог("СуммаНДС");
			КонецЦикла;
			
			ФормаДокумента.ИтогиВсего=		СуммаИтогов;
			
			Если НЕ СокрЛП(ВидОперации)="Продажа, комиссия" И НЕ СокрЛП(ВидОперации)="Услуги"
				И НЕ СокрЛП(ВидОперации)="ПоступлениеДопРасходов" Тогда
			
				ФормаДокумента.ИтогиВсегоНДС=				СуммаНДСИтогов;
				ЕстьМатериальныеЦенности=					ФормаДокумента.Объект.Товары.Количество() > 0 ИЛИ ФормаДокумента.Объект.ВозвратнаяТара.Количество() > 0 ИЛИ ФормаДокумента.Объект.Оборудование.Количество() > 0;
				ФормаДокумента.ОтметкаНезаполненногоСклад= 	ЕстьМатериальныеЦенности;
				ФормаДокумента.ЕстьСтрокиВТабличныхЧастях= 	ЕстьМатериальныеЦенности ИЛИ ФормаДокумента.Объект.Услуги.Количество() > 0 ИЛИ ФормаДокумента.Объект.АгентскиеУслуги.Количество() > 0;
			
			ИначеЕсли СокрЛП(ВидОперации)="Услуги" Тогда
				ФормаДокумента.ИтогиВсегоНДС= СуммаНДСИтогов;
			КонецЕсли;
			
		ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" Тогда
			
			ФормаДокумента.ИтогВсего= 		ДокументОбъект.Запасы.Итог("Всего") 	+ ДокументОбъект.Расходы.Итог("Всего");
			ФормаДокумента.ИтогСуммаНДС= 	ДокументОбъект.Запасы.Итог("СуммаНДС") 	+ ДокументОбъект.Расходы.Итог("СуммаНДС");

		КонецЕсли;
		
	КонецПроцедуры
	
	&НаСервере
	Процедура ЗаполнитьСписокВыбораВидОперации(Элемент)
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
			
			Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика"));
			Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента"));
			Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо"));
			Элемент.СписокВыбора.Добавить("ПоступлениеУслугИПрочихАктивов", "Поступление услуг и прочих активов");
			
			ВидОперации= ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика");
			
		ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
			
			Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия"));
			Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Услуги"));
			Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.ПродажаКомиссия"), "Возврат товаров от покупателя");
			Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку"));
			Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Оборудование"));
			Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства"));
			Элемент.СписокВыбора.Добавить("ПоступлениеДопРасходов", "Поступление доп. расходов");
			
			ВидОперации= ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия");
			
		ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БГУ20" Тогда
			
			Если Параметры.Type = "XmlAcceptanceCertificate" Тогда
				Элемент.СписокВыбора.Добавить("ПоступлениеУслугРабот", "Поступление услуг, работ");
				ВидОперации= "ПоступлениеУслугРабот";
			Иначе
				Элемент.СписокВыбора.Добавить("ПоступлениеМЗ"		 , "Поступление МЗ (М-4)");
				Элемент.СписокВыбора.Добавить("ПоступлениеОС"		 , "Поступление ОС, НМА, НПА");
				ВидОперации= "ПоступлениеМЗ";
			КонецЕсли;
			
		ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" Тогда
		
			Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика"));
			//Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию"));
			//Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ПриемВПереработку"));
			//Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ПриемНаОтветхранение"));
			Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя"));
			//Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтКомиссионера"));
			//Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика"));
			//Элемент.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратСОтветхранения"));
			
			ВидОперации= ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика");

		КонецЕсли;
		
	КонецПроцедуры
		
	&НаКлиенте
	Функция ВидОперацииУслуги(ПараметрВидОперации)
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
			Возврат ПараметрВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Услуги");
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	КонецФункции
	
//} СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ УПРАВЛЕНИЕ ФОРМОЙ И ОБРАБОТКА СОБЫТИЙ
	
	&НаКлиенте
	Процедура НастройкаВидимости()
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11"
			ИЛИ Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
			Заголовок= "Создание документа поступления товаров и услуг";
		Иначе
			
			Если ЗначениеЗаполнено(ВидОперации) Тогда
				Заголовок= "Создание документа " + Элементы.ВидОперации.СписокВыбора.НайтиПоЗначению(ВидОперации).Представление;
			Иначе
				Заголовок= "Создание документа";
			КонецЕсли;
			
		КонецЕсли;
		
		Если СокрЛП(ВидОперации) = "Объекты строительства" Тогда
			Элементы.ТабличнаяЧастьНоменклатура.ОграничениеТипа=	Новый ОписаниеТипов("СправочникСсылка.ОбъектыСтроительства");
			Элементы.ТабличнаяЧастьНоменклатура.Заголовок=			"Объект строительства 1С";
		ИначеЕсли ВидОперации = "ПоступлениеОС" Тогда
			Элементы.ТабличнаяЧастьНоменклатура.ОграничениеТипа=	Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства");
			Элементы.ТабличнаяЧастьНоменклатура.Заголовок=			"Основное средство 1С";
		Иначе
			Элементы.ТабличнаяЧастьНоменклатура.ОграничениеТипа=	Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
			Элементы.ТабличнаяЧастьНоменклатура.Заголовок=			"Номенклатура 1С";
		КонецЕсли;
		
		Если СокрЛП(ВидОперации) = "Объекты строительства" ИЛИ ЭДОбъект.Type = "XmlAcceptanceCertificate"ИЛИ ВидОперации = "ПоступлениеОС" Тогда
			Элементы.ТабличнаяЧастьЕдиницаИзЭД.Видимость=			Ложь;
			Элементы.ТабличнаяЧастьЕдиницаИзЭД.Доступность=			Ложь;
			Элементы.ТабличнаяЧастьЕдиницаИзмерения.Видимость=		Ложь;
			Элементы.ТабличнаяЧастьЕдиницаИзмерения.Доступность=	Ложь;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Контрагент) Тогда
			Элементы.ДоговорыКонтрагентов.Доступность=	Истина;
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
				Элементы.Соглашение.Доступность=	Истина;
			КонецЕсли;
		Иначе
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БГУ20" Тогда
				ДоговорКонтрагента=	ПредопределенноеЗначение("Справочник.Договоры.ПустаяСсылка");
			Иначе
				ДоговорКонтрагента=	ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка");
			КонецЕсли;
			Элементы.ДоговорыКонтрагентов.Доступность=	Ложь;
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
				Соглашение=	ПредопределенноеЗначение("Справочник.СоглашенияСПоставщиками.ПустаяСсылка");
				Элементы.Соглашение.Доступность=	Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ПредставлениеДокумента.Заголовок=	МетодКлиента("Модуль_Клиент","ПредставлениеЭД", ЭДОбъект);
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
			
			Если МетодСервераБезКонтекста(,"ПолучитьФункциональнуюОпциюНаСервере", "ИспользоватьСоглашенияСПоставщиками") = Истина Тогда
				
				Элементы.Соглашение.Видимость= Истина;
				
				Если ЗначениеЗаполнено(Соглашение) И МетодКлиента("Модуль_Клиент", "ПолучитьЗначениеРеквизитаОбъекта", Соглашение, "ИспользуютсяДоговорыКонтрагентов") = Истина Тогда
					Элементы.ДоговорыКонтрагентов.Видимость= Истина;
				Иначе
					Элементы.ДоговорыКонтрагентов.Видимость= Ложь;
					ДоговорКонтрагента=	ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка");
				КонецЕсли;
			
			Иначе
				
				Если МетодСервераБезКонтекста(,"ПолучитьФункциональнуюОпциюНаСервере", "ИспользоватьДоговорыСПоставщиками") = Истина Тогда
					Элементы.ДоговорыКонтрагентов.Видимость= Истина;
				Иначе
					Элементы.ДоговорыКонтрагентов.Видимость= Ложь;
					ДоговорКонтрагента=	ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка");
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЕстьВозможностьЗаполненияГТД(ЭДОбъект) Тогда
			Элементы.ЗаполнениеГТД.Доступность= Истина;
			ЗаполнениеГТД= Истина;
			Элементы.ТабличнаяЧастьСтранаПроисхождения.Доступность= Истина;
		Иначе
			Элементы.ЗаполнениеГТД.Доступность= Ложь;
			ЗаполнениеГТД= Ложь;
			Элементы.ТабличнаяЧастьСтранаПроисхождения.Доступность= Ложь;
		КонецЕсли;				
		
		ОбновитьИспользованиеЕдиницыИзмерения();
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ЕстьВозможностьЗаполненияГТД(Document)
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
			
			Если ВедетсяУчетИмпортныхТоваров
				И  (ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Оборудование")
				ИЛИ ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия"))
				И ПоDocumentЕстьГТД(Document) Тогда
				
				Возврат Истина;
			КонецЕсли;
			
		ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
			
			Если ВедетсяУчетИмпортныхТоваров 
				И (ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика")
				ИЛИ ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо"))
				И ПоDocumentЕстьГТД(Document) Тогда
				
				Возврат Истина;
			КонецЕсли;

		ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" Тогда
			
			Если ВедетсяУчетИмпортныхТоваров
				И (ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика")
				ИЛИ ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя"))
				И ПоDocumentЕстьГТД(Document) Тогда
				
				Возврат Истина;
			КонецЕсли;

		КонецЕсли;
		
		Возврат Ложь;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоDocumentЕстьГТД(Document)
		
		Если Document.Type = "UniversalTransferDocument" Тогда
			
			СтруктураInvoice= ПолучитьСтруктуруСодержанияДокумента(Document);
			Для каждого Item из СтруктураInvoice.Items Цикл
				Если ЗначениеЗаполнено(Item.TDNumber) = Истина Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			СвязанныйInvoice= ПолучитьСвязанныйInvoice(Document);
			Если СвязанныйInvoice = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
			
			СтруктураInvoice= ПолучитьСтруктуруСодержанияДокумента(СвязанныйInvoice);
			Для каждого Item из СтруктураInvoice.Items Цикл
				Если ЗначениеЗаполнено(Item.TDNumber) = Истина Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;	

		КонецЕсли;

		Возврат Ложь;
		
	КонецФункции
		
	&НаКлиенте
	Процедура ПриОткрытии(Отказ)
		
		ПлатформаПриОткрытии(Отказ);
		
		ЭДОбъект=	ЭтаФорма.ВладелецФормы.ЭДОбъект;
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
			
			Попытка
				Элементы.ТабличнаяЧастьЕдиницаИзмерения.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения");
			Исключение
				Элементы.ТабличнаяЧастьЕдиницаИзмерения.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"); //УТ 11.2
			КонецПопытки;
			
		Иначе
			Элементы.ТабличнаяЧастьЕдиницаИзмерения.ОграничениеТипа=	Новый ОписаниеТипов("СправочникСсылка.КлассификаторЕдиницИзмерения");
		КонецЕсли;
		
		СтруктураЭДОбъект=	ЗаполнитьДанныеЭД();
		
		ПриИзмененииШапки(Истина);
		
		НастройкаВидимости();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПриЗакрытии()
		
		ПлатформаПриЗакрытии();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПриИзмененииШапки(ПервоначальноеЗаполнениеТЧ = Ложь)
		
		Если ЗначениеЗаполнено(Контрагент) Тогда
			ДоговорКонтрагента=			УстановитьДоговорКонтрагента(Организация, Контрагент, СписокВидовДоговоров);
			ТекущийДоговорКонтрагента=	ДоговорКонтрагента;
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
				Соглашение=						УстановитьСоглашениеКонтрагента(Организация, Контрагент);
				ТекущееСоглашениеКонтрагента=	Соглашение;
			КонецЕсли;
		КонецЕсли;
		
		УстановитьПараметрыВыбораДоговора();
		
		Если ПервоначальноеЗаполнениеТЧ Тогда
			ЗаполнитьДанныеТЧЗначениямиИз1С(ЭДОбъект.Type);
		Иначе
			Оповещение=	Новый ОписаниеОповещения("ОбработчикВопросЗаполнитьТЧ", ЭтаФорма);
			ПоказатьВопрос(Оповещение, "Заполнить табличную часть значениями по умолчанию?", РежимДиалогаВопрос.ДаНет, 120, КодВозвратаДиалога.Нет, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы, КодВозвратаДиалога.Нет);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикВопросЗаполнитьТЧ(РезультатВопроса, ДополнительныеПараметры) Экспорт
		
		Если РезультатВопроса = КодвозвратаДиалога.Да Тогда
			ЗаполнитьДанныеТЧЗначениямиИз1С(ЭДОбъект.Type);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТабличнаяЧастьПередУдалением(Элемент, Отказ)
		Отказ=	Истина;
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТабличнаяЧастьПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
		Отказ=	Истина;
	КонецПроцедуры
	
	&НаСервере
	Процедура ТабличнаяЧастьНоменклатураПриИзмененииНаСервере(ИдентификаторТекущейСтроки, ТипОбъектаЭДО)
		
		СтрокаТЧ=		ТабличнаяЧасть.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
		
		ИндексСтроки=	ТабличнаяЧасть.Индекс(СтрокаТЧ);
		
		ТабДанных=				РеквизитФормыВЗначение("ТабличнаяЧасть", Тип("ТаблицаЗначений"));
		ИскомаяСтрока=			ТабДанных.Получить(ИндексСтроки);
		ЗаполнитьТипНоменклатуры(ИскомаяСтрока, ТипОбъектаЭДО);
		
		Если ЗначениеЗаполнено(ИскомаяСтрока.Номенклатура) И ИскомаяСтрока.Тип <> "Основное средство" Тогда
			ИскомаяСтрока.ЕдиницаИзмерения=	ИскомаяСтрока.Номенклатура.ЕдиницаИзмерения;
		КонецЕсли;
		
		ЗначениеВРеквизитФормы(ТабДанных, "ТабличнаяЧасть");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТабличнаяЧастьНоменклатураПриИзменении(Элемент)
		
		ТабличнаяЧастьНоменклатураПриИзмененииНаСервере(Элементы.ТабличнаяЧасть.ТекущиеДанные.ПолучитьИдентификатор(), ЭДОбъект.Type);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ДоговорыКонтрагентовПриИзменении(Элемент)
		УстановитьСоответствиеДоговора(ТекущийДоговорКонтрагента, "");
	КонецПроцедуры
	
	&НаКлиенте
	Процедура НадписьСопоставитьС1СНажатие(Элемент)
		
		Параметры.Режим=	"Сопоставление";
		Закрыть(Параметры);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОрганизацияПриИзменении(Элемент)
		
		ПриИзмененииШапки();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СуммаВключаетНДСПриИзменении(Элемент)
		
		СуммаВключаетНДСПриИзмененииНаСервере();
		
	КонецПроцедуры
	
	&НаСервере
	Процедура СуммаВключаетНДСПриИзмененииНаСервере()
		
		ТабДанных=	РеквизитФормыВЗначение("ТабличнаяЧасть", Тип("ТаблицаЗначений"));
		ПересчитатьПоляСУчетомНДС(ТабДанных);
		ЗначениеВРеквизитФормы(ТабДанных, "ТабличнаяЧасть");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ВидОперацииПриИзменении(Элемент)
		
		ИнициализироватьСписокВидовДоговоров();
		УстановитьПараметрыВыбораДоговора();
		НастройкаВидимости();
		ЗаполнитьДанныеТЧЗначениямиИз1С(ЭДОбъект.Type);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СоглашениеПриИзменении(Элемент)
		
		УстановитьСоответствиеСоглашения(ТекущееСоглашениеКонтрагента, "");
		УстановитьПараметрыВыбораДоговора();
		НастройкаВидимости();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КонтрагентПриИзменении(Элемент)
		
		ПриИзмененииШапки();
		НастройкаВидимости();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СоглашениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
		СтандартнаяОбработка = Ложь;
		
		Партнер= ПолучитьПартнера(Контрагент);
		ВыборСоглашенияСКонтрагентом(Партнер, Элемент);
		
	КонецПроцедуры
	
	&НаСервере
	Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
		ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка);
		
		стПрофильКонфигурации=	МетодСервера(,"СформироватьПрофильКонфигурации");
		
		СуммаВключаетНДС= Истина;
		
		ЗаполнитьСписокВыбораВидОперации(Элементы.ВидОперации);
		
		ИнициализироватьСписокВидовДоговоров();
		
		Если СписокВидовОпераций.Количество() > 0 Тогда
			ВидОперации=	СписокВидовОпераций.Получить(0).Значение;
		КонецЕсли;
		
		МассивТипов=	Новый Массив();
		МассивТипов.Добавить("СправочникСсылка.ДоговорыКонтрагентов");
		ОписаниеТипов= 	Новый ОписаниеТипов(МассивТипов);
		Элементы.ДоговорыКонтрагентов.ОграничениеТипа=	ОписаниеТипов;
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
			ИспользоватьЕдиницуИзмерения=	Истина;
		Иначе
			Если НЕ СокрЛП(ВидОперации) = "Объекты строительства" Тогда
				ИспользоватьЕдиницуИзмерения=	Истина;
			Иначе
				ИспользоватьЕдиницуИзмерения=	Ложь;
			КонецЕсли;
		КонецЕсли;
		
		ВалютаРегламентированногоУчета= Константы[Объект.ПараметрыКлиентСервер.ПредставлениеКонстант.ВалютаРеглУчета].Получить();
		
		Контрагент=		Параметры.Контрагент;
		Организация=	Параметры.Организация;
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
			ВедетсяУчетИмпортныхТоваров= Константы.ВедетсяУчетИмпортныхТоваров.Получить();
		ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
			Если Найти(Объект.ПараметрыКлиентСервер.СинонимКонфигурации, "ERP")>0 Тогда
				ВедетсяУчетИмпортныхТоваров= Константы.ИспользоватьИмпортныеЗакупки.Получить();
			Иначе
				Если НЕ Метаданные.Константы.Найти("ИспользоватьИмпортныеТовары") = Неопределено Тогда
					ВедетсяУчетИмпортныхТоваров= Константы.ИспользоватьИмпортныеТовары.Получить();
				ИначеЕсли НЕ Метаданные.Константы.Найти("ИспользоватьИмпортныеЗакупки") = Неопределено Тогда
					ВедетсяУчетИмпортныхТоваров= Константы.ИспользоватьИмпортныеЗакупки.Получить();
				Иначе
					ВедетсяУчетИмпортныхТоваров= Ложь;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" Тогда
			Если Метаданные.Константы.Найти("ФункциональнаяОпцияУчетГТД") = Неопределено Тогда
				ВедетсяУчетИмпортныхТоваров= Ложь;
			Иначе
				ВедетсяУчетИмпортныхТоваров= Константы.ФункциональнаяОпцияУчетГТД.Получить();
			КонецЕсли;
		Иначе
			ВедетсяУчетИмпортныхТоваров= Ложь;
		КонецЕсли;
				
	КонецПроцедуры
	
//} УПРАВЛЕНИЕ ФОРМОЙ И ОБРАБОТКА СОБЫТИЙ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ КОМАНДЫ
	
	&НаКлиенте
	Процедура Создать(Команда)
		
		Если НЕ ПроверитьВсеНеобходимыеДанные() Тогда
			ПоказатьПредупреждение(, "Заполните все необходимые данные.", , Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
			Возврат;
		КонецЕсли;
		
		Если ВидОперацииУслуги(ВидОперации) Тогда
			
			МассивТовары= ТабличнаяЧасть.НайтиСтроки(Новый Структура("Тип", "Товар"));
			Если МассивТовары.Количество()>0 Тогда
				ПоказатьПредупреждение(, "При виде операции ""Услуги"", в табличной части не должно быть номенклатуры с типом ""Товар"".", , Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
				Возврат;				
			КонецЕсли;
			
		КонецЕсли;
		
		ПерейтиКСозданиюДокумента1С();
		
	КонецПроцедуры
	
//} КОМАНДЫ
////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура ОбработчикЗакрытиеФормыНакладной() Экспорт
	
	Если НЕ ФормаДокумента.Открыта() 
		И ЗначениеЗаполнено(ФормаДокумента.Объект.Ссылка) Тогда
		ОтключитьОбработчикОжидания("ОбработчикЗакрытиеФормыНакладной");
		Параметры.Документ1С=	ФормаДокумента.Объект.Ссылка;
		ЭтаФорма.Закрыть(Параметры);
	Иначе
		ПодключитьОбработчикОжидания("ОбработчикЗакрытиеФормыНакладной", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборСоглашенияСКонтрагентом(Партнер, Элемент)
	
	ПарметрыФормы= Новый Структура("ДатаДокумента, ТекущаяСтрока, Отбор", ТекущаяДата(), Соглашение, Новый Структура);
	ПарметрыФормы.Отбор.Вставить("Партнер", Партнер);
	
	Если ВРег(СокрЛП(ВидОперации)) = ВРег("Возврат товаров от клиента")  Тогда
		ПарметрыФормы.Вставить("ДоступноДляПродажиКлиентам", Истина);
		ИмяФормыВыбора= "Справочник.СоглашенияСКлиентами.ФормаВыбора";
	Иначе
		ПарметрыФормы.Вставить("ДоступноДляЗакупки", Истина);
		ИмяФормыВыбора= "Справочник.СоглашенияСПоставщиками.ФормаВыбора";
	КонецЕсли;
	
	МетодКлиента(,"ОткрытьФормуОбъектаИБ",,ИмяФормыВыбора, ПарметрыФормы, Элемент);
	
	ТекущееСоглашениеКонтрагента= Соглашение;
	
КонецПроцедуры

&НаКлиенте
Функция ОпределитьСтавкуНДСПоУслуге(СуммаБезНДС, СуммаНДС)
	
	Если СуммаБезНДС = 0 Тогда
		Возврат 0;
	Конецесли;
	
	расчетнаяСтавка = 100 * СуммаНДС / СуммаБезНДС;
	Если расчетнаяСтавка < 5 Тогда
		Возврат "0";
	ИначеЕсли расчетнаяСтавка < 14 Тогда
		Возврат "10";
	Иначе
		возврат "18";
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ПолучитьСтруктуруСодержанияДокумента(Document) Экспорт
	
	Если Document.Type = "XmlTorg12" Тогда
		
		стДокумент= 		Новый Структура("Document, Type, Items", Document, Document.Type);
		тзТабличнаяЧасть= 	Новый Массив();
		
		DocumentContent= Document.GetContent();
		
		Для Ц=0 по DocumentContent.Items.Count()-1 Цикл
			
			Item= DocumentContent.Items.GetItem(Ц); 
			
			СтрокаТЧ= Новый Структура();
			
			СтрокаТЧ.Вставить("Name", Неопределено);
			СтрокаТЧ.Вставить("Code", Неопределено);
			СтрокаТЧ.Вставить("NomenclatureArticle", Неопределено);
			СтрокаТЧ.Вставить("Quantity", Неопределено);
			СтрокаТЧ.Вставить("UnitCode", Неопределено);
			СтрокаТЧ.Вставить("UnitName", Неопределено);
			СтрокаТЧ.Вставить("Price", Неопределено);
			СтрокаТЧ.Вставить("SubtotalWithVatExcluded", Неопределено);
			СтрокаТЧ.Вставить("Vat", Неопределено);
			СтрокаТЧ.Вставить("Subtotal", Неопределено);
			СтрокаТЧ.Вставить("TaxRate", Неопределено);
			
			СтрокаТЧ.Name= 	?(ЗначениеЗаполнено(Item.Product), Item.Product, Неопределено);
			СтрокаТЧ.Code= 	?(ЗначениеЗаполнено(Item.ProductCode), Item.ProductCode, Неопределено);
			СтрокаТЧ.NomenclatureArticle= ?(ЗначениеЗаполнено(Item.Article), Item.Article, Неопределено);
			СтрокаТЧ.UnitCode= 	?(ЗначениеЗаполнено(Item.UnitCode), Item.UnitCode, Неопределено);
			СтрокаТЧ.UnitName= 	?(ЗначениеЗаполнено(Item.UnitName), Item.UnitName, Неопределено);
			СтрокаТЧ.Quantity=  ?(ЗначениеЗаполнено(Item.Quantity), Item.Quantity, Неопределено);
			СтрокаТЧ.Price= 	?(ЗначениеЗаполнено(Item.Price), Item.Price, Неопределено);
			СтрокаТЧ.SubtotalWithVatExcluded= ?(ЗначениеЗаполнено(Item.TotalWithVatExcluded), Item.TotalWithVatExcluded, Неопределено);
			СтрокаТЧ.Vat= 		?(ЗначениеЗаполнено(Item.Vat), Item.Vat, Неопределено);
			СтрокаТЧ.Subtotal= 	?(ЗначениеЗаполнено(Item.Total), Item.Total, Неопределено);
			СтрокаТЧ.TaxRate= 	?(ЗначениеЗаполнено(Item.TaxRate), Item.TaxRate, Неопределено);
						
			тзТабличнаяЧасть.Добавить(СтрокаТЧ);
			
		КонецЦикла;
		
		стДокумент.Items=	тзТабличнаяЧасть;
		
		стДокумент.Вставить("ДатаДоговора", 		?(ЗначениеЗаполнено(DocumentContent.GroundDate), DocumentContent.GroundDate, ""));
		стДокумент.Вставить("НомерДоговора", 		?(ЗначениеЗаполнено(DocumentContent.GroundNumber), DocumentContent.GroundNumber, ""));
		стДокумент.Вставить("НаименованиеДоговора", ?(ЗначениеЗаполнено(DocumentContent.GroundName), DocumentContent.GroundName, ""));
		
		стДокумент.Вставить("Total", 	Document.Total);
		стДокумент.Вставить("Vat", 		Document.Vat);
			
		Возврат стДокумент;
		
	ИначеЕсли Document.Type = "XmlAcceptanceCertificate" Тогда
		
		стДокумент= 		Новый Структура("Document, Type, Items", Document, "XmlAcceptanceCertificate");
		тзТабличнаяЧасть=	Новый Массив();
		
		DocumentContent = Document.GetContent();
		
		Для Ц=0 по DocumentContent.Items.Count()-1 Цикл
			
			Item= DocumentContent.Items.GetItem(Ц); 
			
			СтрокаТЧ= Новый Структура();
						
			СтрокаТЧ.Вставить("Name", Неопределено);
			СтрокаТЧ.Вставить("Code", Неопределено);
			СтрокаТЧ.Вставить("NomenclatureArticle", Неопределено);
			СтрокаТЧ.Вставить("Quantity", Неопределено);
			СтрокаТЧ.Вставить("UnitCode", Неопределено);
			СтрокаТЧ.Вставить("UnitName", Неопределено);
			СтрокаТЧ.Вставить("Price", Неопределено);
			СтрокаТЧ.Вставить("SubtotalWithVatExcluded", Неопределено);
			СтрокаТЧ.Вставить("Vat", Неопределено);
			СтрокаТЧ.Вставить("Subtotal", Неопределено);
			СтрокаТЧ.Вставить("TaxRate", Неопределено);
			
			Если ЗначениеЗаполнено(Item.Name) Тогда
				СтрокаТЧ.Name= 	Item.Name;
			ИначеЕсли ЗначениеЗаполнено(Item.Description) Тогда
				СтрокаТЧ.Name= 	Item.Description;
			Иначе
				СтрокаТЧ.Name= 	Неопределено;
			КонецЕсли;
			
			СтрокаТЧ.Code= 		"";
			СтрокаТЧ.UnitCode= 	?(ЗначениеЗаполнено(Item.UnitCode), Item.UnitCode, Неопределено);
			СтрокаТЧ.UnitName= 	?(ЗначениеЗаполнено(Item.UnitName), Item.UnitName, Неопределено);
			СтрокаТЧ.Quantity= 	?(ЗначениеЗаполнено(Item.Quantity), ЧислоИзКомпоненты(Item.Quantity), 0);
			СтрокаТЧ.Price= 	?(ЗначениеЗаполнено(Item.Price), ЧислоИзКомпоненты(Item.Price), 0);
			СтрокаТЧ.SubtotalWithVatExcluded= ?(ЗначениеЗаполнено(Item.TotalWithVatExcluded), ЧислоИзКомпоненты(Item.TotalWithVatExcluded), Неопределено);
			СтрокаТЧ.Vat= 		?(ЗначениеЗаполнено(Item.Vat), ЧислоИзКомпоненты(Item.Vat), 0);
			СтрокаТЧ.Subtotal= 	?(ЗначениеЗаполнено(Item.Total), ЧислоИзКомпоненты(Item.Total), 0);
			СтавкаНДС=			ОпределитьСтавкуНДСПоУслуге(?(ЗначениеЗаполнено(Item.TotalWithVatExcluded), Item.TotalWithVatExcluded, 0), ?(ЗначениеЗаполнено(Item.Vat), Item.Vat, 0));
			СтрокаТЧ.TaxRate= 	?(ЗначениеЗаполнено(СтавкаНДС), СтавкаНДС, Неопределено);
			
			Если СтрокаТЧ.TaxRate = "0" Тогда
				СтрокаТЧ.TaxRate = ""; //для того, чтобы выводилась ставка "Без НДС"	
			КонецЕсли;
						
			тзТабличнаяЧасть.Добавить(СтрокаТЧ);
			
		КонецЦикла;
		
		стДокумент.Items = тзТабличнаяЧасть;
		
		стДокумент.Вставить("ДатаДоговора",			"");
		стДокумент.Вставить("НомерДоговора",		"");
		стДокумент.Вставить("НаименованиеДоговора",	"");
				
		стДокумент.Вставить("total", 	Document.total);
		стДокумент.Вставить("vat",		Document.vat);
							
		Возврат стДокумент;
		
	ИначеЕсли Лев(Document.Type, 7) = "Invoice" Тогда
	
		стДокумент= 		Новый Структура("Document, Type, Items", Document, Document.Type);
		тзТабличнаяЧасть= 	Новый Массив();
		
		DocumentContent= Document.GetContent();
		
		Для Ц=0 по DocumentContent.Items.Count()-1 Цикл
			
			Item= DocumentContent.Items.GetItem(Ц); 
			
			СтрокаТЧ= Новый Структура();
			
			СтрокаТЧ.Вставить("LineNumber", Неопределено);
			СтрокаТЧ.Вставить("Name", Неопределено);
			СтрокаТЧ.Вставить("Code", Неопределено);
			СтрокаТЧ.Вставить("NomenclatureArticle", Неопределено);
			СтрокаТЧ.Вставить("Quantity", Неопределено);
			СтрокаТЧ.Вставить("UnitCode", Неопределено);
			СтрокаТЧ.Вставить("UnitName", Неопределено);
			СтрокаТЧ.Вставить("Price", Неопределено);
			СтрокаТЧ.Вставить("SubtotalWithVatExcluded", Неопределено);
			СтрокаТЧ.Вставить("Vat", Неопределено);
			СтрокаТЧ.Вставить("Subtotal", Неопределено);
			СтрокаТЧ.Вставить("TaxRate", Неопределено);
			СтрокаТЧ.Вставить("CountryCode", Неопределено);
			СтрокаТЧ.Вставить("TDNumber", Неопределено);
			
			
			СтрокаТЧ.Name= 			Item.Product;
			СтрокаТЧ.UnitCode= 		Item.UnitCode;
			СтрокаТЧ.Quantity= 		ЧислоИзКомпоненты(Item.Quantity);
			СтрокаТЧ.Price= 		ЧислоИзКомпоненты(Item.Price);
			СтрокаТЧ.SubtotalWithVatExcluded= Item.TotalWithVatExcluded;
			СтрокаТЧ.Subtotal= 		Item.Total;
			СтрокаТЧ.Vat= 			ЧислоИзКомпоненты(Item.Vat);
			СтрокаТЧ.TaxRate= 		Item.TaxRate;
			СтрокаТЧ.CountryCode= 	Item.CountriesOfOrigin;
			СтрокаТЧ.TDNumber= 		Item.CustomsDeclarationNumbers;
			
			тзТабличнаяЧасть.Добавить(СтрокаТЧ);
			
		КонецЦикла;
		
		стДокумент.Items = тзТабличнаяЧасть;
		
		стДокумент.Вставить("ДатаДоговора",			"");
		стДокумент.Вставить("НомерДоговора",		"");
		стДокумент.Вставить("НаименованиеДоговора",	"");
				
		стДокумент.Вставить("total", 	Document.total);
		стДокумент.Вставить("vat",		Document.vat);
		
		Возврат стДокумент;

	ИначеЕсли Document.Type = "UniversalTransferDocument" Тогда
	
		стДокумент= 		Новый Структура("Document, Type, Items", Document, Document.Type);
		тзТабличнаяЧасть= 	Новый Массив();
		
		DocumentContent= Document.GetContent();
		
		Для Ц=0 по DocumentContent.InvoiceTable.Items.Count()-1 Цикл
			
			Item= DocumentContent.InvoiceTable.Items.GetItem(Ц); 
			
			СтрокаТЧ= Новый Структура();
			
			СтрокаТЧ.Вставить("LineNumber", Неопределено);
			СтрокаТЧ.Вставить("Name", Неопределено);
			СтрокаТЧ.Вставить("Code", Неопределено);
			СтрокаТЧ.Вставить("NomenclatureArticle", Неопределено);
			СтрокаТЧ.Вставить("Quantity", Неопределено);
			СтрокаТЧ.Вставить("UnitCode", Неопределено);
			СтрокаТЧ.Вставить("UnitName", Неопределено);
			СтрокаТЧ.Вставить("Price", Неопределено);
			СтрокаТЧ.Вставить("SubtotalWithVatExcluded", Неопределено);
			СтрокаТЧ.Вставить("Vat", Неопределено);
			СтрокаТЧ.Вставить("Subtotal", Неопределено);
			СтрокаТЧ.Вставить("TaxRate", Неопределено);
			СтрокаТЧ.Вставить("CountryCode", Неопределено);
			СтрокаТЧ.Вставить("TDNumber", Неопределено);
			
			СтрокаТЧ.Code= 			?(ЗначениеЗаполнено(Item.VendorCode), Item.VendorCode, Неопределено);
			СтрокаТЧ.Name= 			Item.Product;
			СтрокаТЧ.UnitCode= 		Item.UnitCode;
			СтрокаТЧ.UnitName= 		?(ЗначениеЗаполнено(Item.UnitName), Item.UnitName, Неопределено);
			СтрокаТЧ.Quantity= 		ЧислоИзКомпоненты(Item.Quantity);
			СтрокаТЧ.Price= 		ЧислоИзКомпоненты(Item.Price);
			СтрокаТЧ.SubtotalWithVatExcluded= Item.SubtotalWithVatExcluded;
			СтрокаТЧ.Subtotal= 		Item.Subtotal;
			СтрокаТЧ.Vat= 			ЧислоИзКомпоненты(Item.Vat);
			СтрокаТЧ.TaxRate= 		Item.TaxRate;
			CustomDeclarations = Item.CustomDeclarations;
			КоличествоCustomDeclarations = CustomDeclarations.Count();
			Если КоличествоCustomDeclarations > 0 Тогда
				Если КоличествоCustomDeclarations = 1 Тогда
					СтрокаCustomDeclarations = CustomDeclarations.GetItem(0);
					СтрокаТЧ.CountryCode= 	СтрокаCustomDeclarations.CountryCode;
					СтрокаТЧ.TDNumber= 		СтрокаCustomDeclarations.DeclarationNumber;
				Иначе
					Сообщить("" + СтрокаТЧ.Name + ", количество = " + СтрокаТЧ.Quantity + ", цена = " + СтрокаТЧ.Price + ": указано более одного значения ""CustomsDeclaration"", данные по ГТД в строке не заполнены!");
				КонецЕсли;
			КонецЕсли;
				
			тзТабличнаяЧасть.Добавить(СтрокаТЧ);
			
		КонецЦикла;
		
		стДокумент.Items = тзТабличнаяЧасть;
		
		стДокумент.Вставить("ДатаДоговора",			"");
		стДокумент.Вставить("НомерДоговора",		"");
		стДокумент.Вставить("НаименованиеДоговора",	"");
				
		стДокумент.Вставить("total", 	Document.total);
		стДокумент.Вставить("vat",		Document.vat);
		
		Возврат стДокумент;

	Иначе
		ВызватьИсключение "Неизвестный Type объекта ЭДОбъект: """ + Document.Type + """";
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ЧислоИзКомпоненты(Значение)
	
	Если ПустаяСтрока(Значение) Тогда
		Возврат 0;
	ИначеЕсли Значение = "без НДС" Тогда
		Возврат 0;
	Иначе	
		Возврат Число(Значение);
	КонецЕсли;	
	
КонецФункции

&НаКлиенте
Функция ЗаполнитьДанныеЭД()

	ТабличнаяЧасть.Очистить();
	
	НомерСтроки=	0;
	
	СтруктураЭДОбъект= ПолучитьСтруктуруСодержанияДокумента(ЭДОбъект);	
	
	СоответствиеСтрокСГТД= ПолучитьСоответствиеСтрокСГТД(СтруктураЭДОбъект); 
	Для Каждого СтрокаDocument Из СтруктураЭДОбъект.Items Цикл
		
		Если СоответствиеСтрокСГТД = Неопределено ИЛИ СоответствиеСтрокСГТД[СтрокаDocument].МассивСтрокиItemInvoice.Количество() = 0 Тогда 
			НомерСтроки=	НомерСтроки + 1;
			НоваяСтрока1С=	ТабличнаяЧасть.Добавить();
			НоваяСтрока1С.НомерСтроки=	НомерСтроки;
			
			ЗаполнитьСтрокуСозданияВ1СДаннымиЭД(НоваяСтрока1С, СтрокаDocument);
		Иначе
			СоздатьСтрокиВТЧДля1СПоСтрокамГТД(ТабличнаяЧасть, СтрокаDocument, СоответствиеСтрокСГТД[СтрокаDocument]);	
		КонецЕсли;
	КонецЦикла;	
		
	ДатаДоговора=			?(Строка(СтруктураЭДОбъект.ДатаДоговора) = "", "", Строка(СтруктураЭДОбъект.ДатаДоговора) + "_");
	НомерДоговора=			?(Строка(СтруктураЭДОбъект.НомерДоговора) = "", "", Строка(СтруктураЭДОбъект.НомерДоговора) + "_");
	НаименованиеДоговора=	?(Строка(СтруктураЭДОбъект.НаименованиеДоговора) = "", "", Строка(СтруктураЭДОбъект.НаименованиеДоговора));
		
	ДоговорСвойство=		Прав(СокрЛП(ДатаДоговора + НомерДоговора + НаименованиеДоговора),50);

	СтруктураЭДОбъект.Вставить("ДоговорСвойство", ДоговорСвойство);
	
	Возврат СтруктураЭДОбъект;
	
КонецФункции

&НаКлиенте
Функция ПолучитьСоответствиеСтрокСГТД(СтруктураDocument)
	
	Если СтруктураDocument.Type = "UniversalTransferDocument" Тогда
		
		СоответствиеГТД= Новый Соответствие;
		
		Для каждого СтрокаDocument из СтруктураDocument.Items Цикл
			МассивСтрокиItemInvoice = Новый Массив;
			Если СтрокаDocument.Quantity > 0 Тогда
				МассивСтрокиItemInvoice.Добавить(СтрокаDocument);
			КонецЕсли;
			СоответствиеГТД.Вставить(СтрокаDocument, Новый Структура("МассивСтрокиItemInvoice, ИтоговоеКоличество", МассивСтрокиItemInvoice, СтрокаDocument.Quantity));
		КонецЦикла;
		
	Иначе
		
		СвязанныйInvoice= ПолучитьСвязанныйInvoice(СтруктураDocument.Document);
		Если СвязанныйInvoice = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		СтруктураInvoice= ПолучитьСтруктуруСодержанияДокумента(СвязанныйInvoice);
		
		ContentItemsInvoice= СтруктураInvoice.Items;
		
		ПустаяСтруктураItemInvoice= Новый Структура;
		Для каждого Элемент из ContentItemsInvoice[0] Цикл
			ПустаяСтруктураItemInvoice.Вставить(Элемент.Ключ, Неопределено);	
		КонецЦикла;
		
		СоответствиеГТД= Новый Соответствие;
		
		Для каждого СтрокаDocument из СтруктураDocument.Items Цикл
			СоответствиеГТД.Вставить(СтрокаDocument, Новый Структура("МассивСтрокиItemInvoice, ИтоговоеКоличество, ПустаяСтруктураItemInvoice", Новый Массив, 0, ПустаяСтруктураItemInvoice));
		КонецЦикла;
		
		НеОбработанныеInvoiceItems= ContentItemsInvoice;
		Для каждого СтрокаDocument из СтруктураDocument.Items Цикл
			Если НеОбработанныеInvoiceItems.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
			
			СтруктрураОтделенныеInvoiceItems= ОтделитьСтрокиВInvoiceДляNameИQuantity(НеОбработанныеInvoiceItems, СтрокаDocument.Name, СтрокаDocument.Quantity );
			ОтделенныеInvoiceItems= 	СтруктрураОтделенныеInvoiceItems.МассивОтделенныхСтрок;
			ОбщееКоличествоВОтделенных= СтруктрураОтделенныеInvoiceItems.ОбщееКоличествоВОтделенных;
			
			Если ОбщееКоличествоВОтделенных = СтрокаDocument.Quantity Тогда
				СоответствиеГТД[СтрокаDocument]= Новый Структура("МассивСтрокиItemInvoice, ИтоговоеКоличество, ПустаяСтруктураItemInvoice", ОтделенныеInvoiceItems, ОбщееКоличествоВОтделенных, ПустаяСтруктураItemInvoice);	
			КонецЕсли;
			
		КонецЦикла;

	КонецЕсли;

	Возврат СоответствиеГТД;
	
КонецФункции

&НаКлиенте
Функция ОтделитьСтрокиВInvoiceДляNameИQuantity(НеОбработанныеInvoiceItems, DocumentItem_Name, DocumentItem_Quantity)
	
	МассивОтделенныхСтрок= Новый Массив;
	ОбщееКоличество= 0;
	
	Пока НеОбработанныеInvoiceItems.Количество()>0 Цикл	
		
		СтрокаInvoice = НеОбработанныеInvoiceItems[0]; 
		
		Если ОбщееКоличество >= DocumentItem_Quantity Тогда
			Прервать;
		КонецЕсли;
		Если НЕ СтрокиПодобны(СтрокаInvoice.Name, DocumentItem_Name) Тогда
			Прервать;			
		КонецЕсли;
		
		СтрокаОтделенных = Новый Структура;
		Для каждого КлючЗначение из СтрокаInvoice Цикл
			СтрокаОтделенных.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЦикла;
		МассивОтделенныхСтрок.Добавить(СтрокаОтделенных);
		
		ОбщееКоличество = ОбщееКоличество + Число(СтрокаОтделенных["Quantity"]);
		
		НеОбработанныеInvoiceItems.Удалить(0);
	КонецЦикла;
	
	Возврат Новый Структура("МассивОтделенныхСтрок, ОбщееКоличествоВОтделенных", МассивОтделенныхСтрок, ОбщееКоличество);
	
КонецФункции

&НаКлиенте
Функция СтрокиПодобны(Строка1, Строка2)
	
	МинимальнаяДлина= Мин(СтрДлина(Строка1), СтрДлина(Строка2));
	Если МинимальнаяДлина = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат НРег(Лев(Строка1, МинимальнаяДлина)) = НРег(Лев(Строка2, МинимальнаяДлина));
			
КонецФункции

&НаКлиенте
Функция ПолучитьСвязанныйInvoice(Document)
	
	InitialDocumentIds= 	Document.InitialDocumentIds;
	SubordinateDocumentIds= Document.SubordinateDocumentIds;
	
	Если InitialDocumentIds.Count = 0
		И SubordinateDocumentIds.Count = 0 Тогда
		
		Возврат Неопределено;
	КонецЕсли;
	
	Organization= 	Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationById(Document.OrganizationId);
	TimeStamp= 		'20010101';
	
	Для Ц=0 по InitialDocumentIds.Count - 1 Цикл
		
		InitialDocumentId= InitialDocumentIds.GetItem(Ц);
		
		Попытка
			InitialDocument= Organization.GetDocumentById(InitialDocumentId);
		Исключение
			ВызватьИсключение("Не удалось получить родительский документ с сервера Диадок");
		КонецПопытки;
		
		// берем последнюю по времени СФ
		Если InitialDocument.Type = "Invoice"
			И TimeStamp <= InitialDocument.TimeStamp Тогда
			
			TimeStamp= 			InitialDocument.TimeStamp; 
			СвязанныйInvoice= 	InitialDocument; 
			
		КонецЕсли;			
		
	КонецЦикла;
	
	Для Ц=0 по SubordinateDocumentIds.Count - 1 Цикл
		
		SubordinateDocumentId= SubordinateDocumentIds.GetItem(Ц);
		
		Попытка
			SubordinateDocument= Organization.GetDocumentById(SubordinateDocumentId);
		Исключение
			ВызватьИсключение("Не удалось получить подчиненный документ с сервера Диадок");
		КонецПопытки;
		
		// берем последнюю по времени СФ
		Если SubordinateDocument.Type = "Invoice"
			И TimeStamp <= SubordinateDocument.TimeStamp Тогда
			
			TimeStamp= 			SubordinateDocument.TimeStamp; 
			СвязанныйInvoice= 	SubordinateDocument; 
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СвязанныйInvoice;		
	
КонецФункции

&НаКлиенте
Процедура СоздатьСтрокиВТЧДля1СПоСтрокамГТД(ТабличнаяЧасть, СтрокаDocument, СтруктураОтобранныеInvoiceItem)
	
	СтрокаДля1С= Неопределено;
	
	InvoiceItemsПоГТД= 				СтруктураОтобранныеInvoiceItem.МассивСтрокиItemInvoice;
	ИтоговоеКоличествоВОтделенных= 	СтруктураОтобранныеInvoiceItem.ИтоговоеКоличество; 
	
	Для каждого СтрокаInvoice из InvoiceItemsПоГТД Цикл
		
		СтрокаДля1С= ТабличнаяЧасть.Добавить();
		
		СтрокаДля1С.НоменклатураИзЭД=	СтрокаDocument.Name;
		СтрокаДля1С.ЕдиницаИзЭД=		СтрокаDocument.UnitName;
		СтрокаДля1С.Количество=			СтрокаInvoice.Quantity;
		СтрокаДля1С.СуммаНДС=			?(ЗначениеЗаполнено(СтрокаDocument.Vat) И ИтоговоеКоличествоВОтделенных <> 0, СтрокаDocument.Vat*СтрокаInvoice.Quantity/ИтоговоеКоличествоВОтделенных, 0);
		СтрокаДля1С.Всего=				?(ИтоговоеКоличествоВОтделенных <> 0, СтрокаDocument.Subtotal*СтрокаInvoice.Quantity/ИтоговоеКоличествоВОтделенных, 0);
		
		СтрокаДля1С.ГТД=				СтрокаInvoice.TDNumber;
		
		СтрокаДля1С.СтранаПроисхожденияЭД= 	МетодСервераБезКонтекста("Модуль_ИнтеграцияУниверсальный", "НайтиСтрануПоКодуВКлассификаторе", СтрокаInvoice.CountryCode);  
		СтрокаДля1С.СтранаПроисхождения= 	МетодСервераБезКонтекста("Модуль_ИнтеграцияУниверсальный", "НайтиСтрануПоКоду", СтрокаInvoice.CountryCode); 
	
		XmlTorg12_Item=	Новый СписокЗначений;
		XmlTorg12_Item.Добавить(СтрокаDocument, "СтрокаЭДОбъект");
	
		СтрокаДля1С.XmlTorg12_Item=		XmlTorg12_Item;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтрокуСозданияВ1СДаннымиЭД(СтрокаДля1С, СтрокаЭДОбъект)
	
	СтрокаДля1С.НоменклатураИзЭД=							СтрокаЭДОбъект.Name;
	СтрокаДля1С.АртикулЭД=									СтрокаЭДОбъект.Code;
	СтрокаДля1С.ЕдиницаИзЭД=								СтрокаЭДОбъект.UnitName;
	СтрокаДля1С.Количество=									СтрокаЭДОбъект.Quantity;
	СтрокаДля1С.СуммаНДС=									СтрокаЭДОбъект.Vat;
	СтрокаДля1С.Всего=										СтрокаЭДОбъект.Subtotal;
	
	XmlTorg12_Item=	Новый СписокЗначений;
	XmlTorg12_Item.Добавить(СтрокаЭДОбъект, "СтрокаЭДОбъект");
	
	СтрокаДля1С.XmlTorg12_Item=								XmlTorg12_Item;
	
КонецПроцедуры

&НаСервере
Функция УстановитьСоглашениеКонтрагента(Организация, Контрагент)
	
	Запрос=	Новый Запрос;
	Запрос.Текст=
		"ВЫБРАТЬ
		|	ЗначенияСвойствОбъектов.Объект КАК Соглашение
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ЗначенияСвойствОбъектов
		|ГДЕ
		|	ЗначенияСвойствОбъектов.Свойство = &Свойство
		|	И ЗначенияСвойствОбъектов.Значение = &Значение";
		
	Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("DDPact"));
	Запрос.УстановитьПараметр("Значение", СокрЛП(Контрагент.УникальныйИдентификатор()));
		
	Выборка=	Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Соглашение;
	Иначе
		
		ВыборкаСоглашений=		МетодСервера(,"ВыбратьОсновноеСоглашениеКонтрагента", Контрагент, Организация);
		Если ВыборкаСоглашений.Следующий() Тогда
			Возврат ВыборкаСоглашений.Соглашение;
		КонецЕсли;
		
		Возврат Вычислить("Справочники.СоглашенияСПоставщиками.ПустаяСсылка()");
		
	КонецЕсли;

КонецФункции

&НаСервере
Функция УстановитьДоговорКонтрагента(Организация, Контрагент, СписокВидовДоговоров)
	
	НайденныйДоговор= Неопределено;
	
	Если ЗначениеЗаполнено(ДоговорСвойство) = Истина Тогда
		
		Запрос=	Новый Запрос;
		Запрос.Текст=
			"ВЫБРАТЬ
			|	ЗначенияСвойствОбъектов.Объект КАК Договор
			|ИЗ
			|	РегистрСведений.ДополнительныеСведения КАК ЗначенияСвойствОбъектов
			|ГДЕ
			|	ЗначенияСвойствОбъектов.Свойство = &Свойство
			|	И ЗначенияСвойствОбъектов.Значение = &Значение";
			
		Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("DDContract"));
		Запрос.УстановитьПараметр("Значение", ДоговорСвойство);
			
		Выборка=	Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			НайденныйДоговор= Выборка.Договор;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НайденныйДоговор = Неопределено Тогда
		НайденныйДоговор= ВыбратьОсновнойДоговорКонтрагента(Организация, Контрагент, СписокВидовДоговоров); 
	КонецЕсли;
	
	Возврат НайденныйДоговор;
	
КонецФункции

&НаСервере
Функция ВыбратьОсновнойДоговорКонтрагента(Организация, Контрагент, СписокВидовДоговоров)
	
	ВыборкаДоговоров= МетодСервера(,"ВыбратьОсновнойДоговорКонтрагента", Контрагент, Организация, СписокВидовДоговоров);
	Если ВыборкаДоговоров.Следующий() Тогда
		Если ВыборкаДоговоров.ПриоритетДоговора = 1 Тогда
			Возврат ВыборкаДоговоров.Договор;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БГУ20" Тогда
		Возврат Справочники.Договоры.ПустаяСсылка();
	Иначе
		Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура УстановитьПараметрыВыбораДоговора()
	
	НовыйМассивПараметров=	Новый Массив();
	Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
		МассивВидовДоговоров=	Новый ФиксированныйМассив(СписокВидовДоговоров.ВыгрузитьЗначения());
		НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ВидДоговора", МассивВидовДоговоров));
		НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Владелец", Контрагент));
	ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
		НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Соглашение", Соглашение));
		НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Партнер", ПолучитьПартнера(Контрагент)));
		НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ВалютаВзаиморасчетов", ПолучитьВалютуПоСоглашению(Соглашение)));
		НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Контрагент", Контрагент));
		НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ХозяйственнаяОперация", ВидОперации));
		НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления", Ложь));
		НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Статус", ПредопределенноеЗначение("Перечисление.СтатусыДоговоровКонтрагентов.Действует")));
	КонецЕсли;
	
	НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Организация", Организация));
	
	НовыеПараметрыВыбора=	Новый ФиксированныйМассив(НовыйМассивПараметров);
	Элементы.ДоговорыКонтрагентов.ПараметрыВыбора=	НовыеПараметрыВыбора;

КонецПроцедуры

&НаСервере
Процедура ПересчитатьПоляСУчетомНДС(ТабДанных)

	Для Каждого СтрокаТЧ Из ТабДанных Цикл
		
		Item=	СтрокаТЧ.XmlTorg12_Item.Получить(0).Значение;
		
		Если СуммаВключаетНДС Тогда
			СтрокаТЧ.Сумма=	Item.Subtotal;
		Иначе
			СтрокаТЧ.Сумма=	Item.SubtotalWithVatExcluded;
		КонецЕсли;
		
		СтрокаТЧ.Цена=	?(СтрокаТЧ.Количество = 0, СтрокаТЧ.Сумма, СтрокаТЧ.Сумма/СтрокаТЧ.Количество);
		
	КонецЦикла;
	
	ИтогКоличество=	Табданных.Итог("Количество");
	ИтогСумма=		Табданных.Итог("Сумма");
	ИтогСуммаНДС=	Табданных.Итог("СуммаНДС");
	ИтогВсего=		Табданных.Итог("Всего");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеТЧЗначениямиИз1С(ТипОбъектаЭДО)
	
	ТабДанных=	РеквизитФормыВЗначение("ТабличнаяЧасть", Тип("ТаблицаЗначений"));
	
	Для Каждого СтрокаТЧ Из ТабДанных Цикл
		ЗаполнитьСтрокуДанными1С(СтрокаТЧ);
		ЗаполнитьТипНоменклатуры(СтрокаТЧ, ТипОбъектаЭДО);
	КонецЦикла;
	
	ПересчитатьПоляСУчетомНДС(ТабДанных);
	
	ЗначениеВРеквизитФормы(ТабДанных, "ТабличнаяЧасть");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТипНоменклатуры(СтрокаТЧ, ТипОбъектаЭДО)
	
	Если НЕ ТипЗнч(СтрокаТЧ.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
		ЭтоУслуга=	Ложь;
	Иначе 	
		ЭтоУслуга=	?(ЗначениеЗаполнено(СтрокаТЧ.Номенклатура), МетодСервера(,"НоменклатураЯвляетсяУслугой", СтрокаТЧ.Номенклатура), Ложь);
	КонецЕсли;	
	
	Если  СокрЛП(ВидОперации) = "Оборудование" Тогда
		СтрокаТЧ.Тип=	"Оборудование";
	ИначеЕсли ВидОперации = "ПоступлениеОС" Тогда
		СтрокаТЧ.Тип= "Основное средство";
	ИначеЕсли СокрЛП(ВидОперации) = "Объекты строительства" Тогда
		СтрокаТЧ.Тип = "Объект стр";
		СтрокаТЧ.Номенклатура=	ПредопределенноеЗначение("Справочник.ОбъектыСтроительства.ПустаяСсылка");
	ИначеЕсли ТипОбъектаЭДО = "XmlAcceptanceCertificate" ИЛИ ЭтоУслуга Тогда
		СтрокаТЧ.Тип=	"Услуга";
	Иначе 
		СтрокаТЧ.Тип=	"Товар";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НайтиНоменклатуруПоставщика(Направление, Контрагент, НоменклатураИзЭД=Неопределено, НоменклатураСсылка=Неопределено, КодНоменклатуры, АртикулНоменклатуры)
	
	Запрос=	Новый Запрос;
	Запрос.Текст=	
		"ВЫБРАТЬ
		|	НоменклатураПоставщиков.Ссылка КАК НоменклатураПоставщика,
		|	НоменклатураПоставщиков.Номенклатура
		|ИЗ
		|	Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &НоменклатураИзЭД = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ НоменклатураПоставщиков.Наименование = &НоменклатураИзЭД
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Номенклатура = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ НоменклатураПоставщиков.Номенклатура = &Номенклатура
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Идентификатор = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ НоменклатураПоставщиков.Идентификатор = &Идентификатор
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Артикул = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ НоменклатураПоставщиков.Артикул = &Артикул
		|		КОНЕЦ
		|	И НоменклатураПоставщиков.Владелец = &Владелец";
		
	Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
		Владелец=	Вычислить("?(ЗначениеЗаполнено(Контрагент.Партнер), Контрагент.Партнер, Справочники.Партнеры.ПустаяСсылка())");
	Иначе
		Владелец=	Вычислить("?(ЗначениеЗаполнено(Контрагент), Контрагент, Справочники.Контрагенты.ПустаяСсылка())");
	КонецЕсли;
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	Если Направление = "ПолучитьНоменклатуру" Тогда
		
		НоменклатураИзЭД = ?(ЗначениеЗаполнено(НоменклатураИзЭД)=Истина, НоменклатураИзЭД, Неопределено);
		Запрос.УстановитьПараметр("НоменклатураИзЭД", НоменклатураИзЭД);
		Запрос.УстановитьПараметр("Номенклатура", Неопределено);		
		
	ИначеЕсли Направление = "УстановитьНоменклатуру" Тогда
		
		НоменклатураСсылка = ?(ЗначениеЗаполнено(НоменклатураСсылка)=Истина, НоменклатураСсылка, Неопределено);
		Запрос.УстановитьПараметр("Номенклатура", НоменклатураСсылка);
		Запрос.УстановитьПараметр("НоменклатураИзЭД", Неопределено);		
		
	Иначе
		Запрос.УстановитьПараметр("НоменклатураИзЭД", Неопределено);
		Запрос.УстановитьПараметр("Номенклатура", Неопределено);		
	КонецЕсли;
	
	
	//Сначала поиск по идентификатору потом по артикулу
	Если ЗначениеЗаполнено(КодНоменклатуры) Тогда
		Запрос.УстановитьПараметр("Идентификатор", КодНоменклатуры);
		Запрос.УстановитьПараметр("Артикул", Неопределено);
	ИначеЕсли НЕ ЗначениеЗаполнено(КодНоменклатуры) И ЗначениеЗаполнено(АртикулНоменклатуры) Тогда
		Запрос.УстановитьПараметр("Идентификатор", Неопределено);
		Запрос.УстановитьПараметр("Артикул", АртикулНоменклатуры);
	Иначе
		Запрос.УстановитьПараметр("Идентификатор", Неопределено);
		Запрос.УстановитьПараметр("Артикул", Неопределено);
	КонецЕсли;
		
	Возврат Запрос.Выполнить();
	
КонецФункции

&НаСервере
Процедура УстановитьНоменклатуруПоставщика(Номенклатура, Контрагент, КодНоменклатуры, АртикулНоменклатуры, Наименование) Экспорт
	
	Если НЕ ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
		Возврат;
	КонецЕсли;	
	
	Если стПрофильКонфигурации.ХранениеНоменклатурыПоставщиков.Вариант = "Справочник_НоменклатураПоставщиков" Тогда
		
		РезультатПоиска = НайтиНоменклатуруПоставщика("УстановитьНоменклатуру", Контрагент, , Номенклатура, КодНоменклатуры, АртикулНоменклатуры);
				
		Если НЕ РезультатПоиска.Пустой() Тогда
			Выборка=	РезультатПоиска.Выбрать();
			Выборка.Следующий();
			Если НЕ Выборка.Номенклатура = Номенклатура Тогда
				ЭлементОбъект=				Выборка.НоменклатураПоставщика.ПолучитьОбъект();
				ЭлементОбъект.Номенклатура=	Номенклатура;
				ЭлементОбъект.Записать();
			КонецЕсли;
		Иначе
			ЭлементОбъект=						Вычислить("Справочники.НоменклатураПоставщиков.СоздатьЭлемент()");
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
				ЭлементОбъект.Владелец=			Вычислить("Контрагент.Партнер");
			Иначе
				ЭлементОбъект.Владелец=			Контрагент;
			КонецЕсли;
			ЭлементОбъект.Наименование=		Наименование; 
			ЭлементОбъект.Номенклатура=		Номенклатура;
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" 
				И НЕ ЗначениеЗаполнено(КодНоменклатуры) Тогда
				ЭлементОбъект.Идентификатор= Новый УникальныйИдентификатор;
			Иначе
				ЭлементОбъект.Идентификатор= КодНоменклатуры;
			КонецЕсли;
			ЭлементОбъект.Артикул=			АртикулНоменклатуры;
			ЭлементОбъект.Записать();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьНоменклатуруПоставщика(Контрагент, КодНоменклатуры, АртикулНоменклатуры, НоменклатураИзЭД=Неопределено) Экспорт
	
	КодНоменклатуры=		?(НЕ ЗначениеЗаполнено(КодНоменклатуры), Неопределено, КодНоменклатуры);
	АртикулНоменклатуры=	?(НЕ ЗначениеЗаполнено(АртикулНоменклатуры), Неопределено, АртикулНоменклатуры);
	
	НоменклатураИзЭД = ?(ЗначениеЗаполнено(НоменклатураИзЭД)=Ложь, Неопределено, НоменклатураИзЭД);	
		
	Если стПрофильКонфигурации.ХранениеНоменклатурыПоставщиков.Вариант = "Справочник_НоменклатураПоставщиков" Тогда
		
		РезультатПоиска=	НайтиНоменклатуруПоставщика("ПолучитьНоменклатуру", Контрагент, НоменклатураИзЭД, , КодНоменклатуры, АртикулНоменклатуры);
			
		Если НЕ РезультатПоиска.Пустой() Тогда
			Выборка=	РезультатПоиска.Выбрать();
			Если Выборка.Количество() = 1 Тогда
				Выборка.Следующий();
				Возврат Выборка.Номенклатура;
			Иначе
				Возврат Неопределено;
			КонецЕсли;
		Иначе 
			Возврат Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

&НаСервере
Функция ПолучитьНоменклатуруПоАртикулу(АртикулНоменклатуры) Экспорт
	
	Если ЗначениеЗаполнено(АртикулНоменклатуры)=Ложь Тогда
		возврат неопределено 
	ИначеЕсли стПрофильКонфигурации.ЕстьАртикул Тогда
		
		Запрос=			Новый Запрос;
		Запрос.Текст=
			"ВЫБРАТЬ
			|	Номенклатура.Ссылка
			|ИЗ
			|	Справочник.Номенклатура КАК Номенклатура
			|ГДЕ
			|	Номенклатура.Артикул = &АртикулНоменклатуры";
		
		Запрос.УстановитьПараметр("АртикулНоменклатуры", АртикулНоменклатуры);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество() = 1 Тогда
			Выборка.Следующий();
			Возврат Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ItemStruct_2_ЕдиницаИзмерения(XmlTorg12_Item, Номенклатура) Экспорт
	
	Если  XmlTorg12_Item = Неопределено Тогда
		Возврат МетодСервера(,"ПолучитьЕдиницуИзмерения", "", "", Номенклатура);
 	Иначе	
		Возврат МетодСервера(,"ПолучитьЕдиницуИзмерения", XmlTorg12_Item.UnitCode, XmlTorg12_Item.UnitName, Номенклатура);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьИспользованиеЕдиницыИзмерения()
	
	Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
		ИспользоватьЕдиницуИзмерения=	Истина;
	Иначе
		Если НЕ СокрЛП(ВидОперации) = "Объекты строительства" Тогда
			ИспользоватьЕдиницуИзмерения=	Истина;
		Иначе
			ИспользоватьЕдиницуИзмерения=	Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьСписокВидовДоговоров()
	
    СписокВидовДоговоров= МетодСервера(,"ПолучитьСписокВидовДоговоров", СокрЛП(ВидОперации)="Продажа, комиссия");

КонецПроцедуры	

&НаСервереБезКонтекста
Функция ПолучитьПартнера(Контрагент)
	Возврат ?(ЗначениеЗаполнено(Контрагент), Контрагент.Партнер, Неопределено);
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеСчетовУчетаМЗ(Организация)
	
	СписокСчетовУчета=	Новый СписокЗначений;
	ВидыСубконтоСчетов=	Вычислить("УправлениеМатериальнымиЗапасами.ПолучитьДанныеСчетовУчетаМЗ(Организация, СписокСчетовУчета)");

	Возврат Новый Структура("СписокСчетовУчета, ВидыСубконтоСчетов", СписокСчетовУчета, ВидыСубконтоСчетов);
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеПоДоговору(ДоговорКонтрагента) Экспорт
	
	Попытка
		Возврат Вычислить("ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДоговорКонтрагента, ""БанковскийСчет, БанковскийСчетКонтрагента"")");
	Исключение
		Возврат Вычислить("ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорКонтрагента, ""БанковскийСчет, БанковскийСчетКонтрагента"")");
	КонецПопытки;
		
КонецФункции

&НаСервере
Процедура УстановитьНастройкуПользователя(Наименование, Значение)
	
	МетодСервера(,"УстановитьНастройкуПользователя", Наименование, Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличнаяЧастьНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ВидОперации = "ПоступлениеОС" Тогда
		
		СтандартнаяОбработка= Ложь;
		
		ПараметрыФормы= Новый Структура("ТекущаяСтрока", Элементы.ТабличнаяЧасть.ТекущиеДанные.Номенклатура);
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Организация", Организация));
		ОткрытьФорму("Справочник.ОсновныеСредства.ФормаВыбора", ПараметрыФормы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКурсВалюты(ДоговорКонтрагента, ДатаКурса)
	
	Возврат	РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ДатаКурса, Новый Структура("Валюта", ДоговорКонтрагента.ВалютаВзаиморасчетов));
	
КонецФункции

&НаСервере
Функция ПолучитьВалютуПоДоговору(ДоговорКонтрагента)
	
	Возврат ДоговорКонтрагента.ВалютаВзаиморасчетов
	
КонецФункции

&НаСервере
Функция ПолучитьВалютуПоСоглашению(Соглашение)
	
	Возврат ?(ЗначениеЗаполнено(Соглашение), Соглашение.Валюта, Неопределено);
	
КонецФункции
