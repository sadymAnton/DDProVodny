
#Область ПЕРМЕННЫЕ_ПЛАТФОРМЫ

&НаКлиенте
Перем Платформа Экспорт;

&НаСервере
Перем ОбработкаОбъект;

#КонецОбласти

#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ_ПЛАТФОРМЫ

&НаКлиенте
Функция МетодКлиента(ИмяМодуля= "", ИмяМетода, 
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL,
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат  Платформа.МетодКлиента(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаКлиенте
Функция МетодСервераБезКонтекста(ИмяМодуля= "", ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат Платформа.МетодСервераБезКонтекста(ИмяМодуля, ИмяМетода,
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция МетодСервера(Знач ИмяМодуля= "", Знач ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат ОбработкаОбъект().МетодСервера(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция ОбработкаОбъект() Экспорт
	
	Если ОбработкаОбъект = Неопределено Тогда
		
		СтруктураОбработки= ПолучитьИзВременногоХранилища(Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
		
		Если СтруктураОбработки <> Неопределено Тогда
			ОбработкаОбъект= СтруктураОбработки.ОбработкаОбъект;
		КонецЕсли;
		
		Если ОбработкаОбъект = Неопределено Тогда
			
			ОбработкаОбъект= РеквизитФормыВЗначение("Объект");
			
			Попытка
				ПоместитьВоВременноеХранилище(Новый Структура("ОбработкаОбъект", ОбработкаОбъект), Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
			Исключение КонецПопытки;
		
		Иначе
			ОбработкаОбъект.ПараметрыКлиентСервер= Объект.ПараметрыКлиентСервер;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОбработкаОбъект;
	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма(ТекущийВладелецФормы)
	
	Если ТекущийВладелецФормы = Неопределено Тогда
		Возврат Неопределено
	ИначеЕсли Прав(ТекущийВладелецФормы.ИмяФормы, 14) = "Форма_Основная" Тогда
		Возврат ТекущийВладелецФормы;
	Иначе
		Возврат ОсновнаяФорма(ТекущийВладелецФормы.ВладелецФормы);
	КонецЕсли;
	
КонецФункции


&НаСервере
Процедура ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", Объект.ПараметрыКлиентСервер);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатформаПриОткрытии(Отказ)
	
	ОсновнаяФорма= ОсновнаяФорма(ВладелецФормы);
	
	Если ОсновнаяФорма <> Неопределено Тогда
		Платформа= ОсновнаяФорма.Платформа;
	КонецЕсли;
		
	Платформа.ПриОткрытииФормыОбработки(ЭтаФорма, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатформаПриЗакрытии()
	
	Платформа.ПриЗакрытииФормыОбработки(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОтказатьВПодписи(Команда)
	
	Если НЕ ЗначениеЗаполнено(Комментарий) 
		И (Параметры.РежимИспользования = 1 ИЛИ Параметры.РежимИспользования = 4) Тогда
		ПоказатьПредупреждение(, "Укажите причину отказа", 120, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
	Иначе
		Параметры.Комментарий=	Комментарий;
		Закрыть(Параметры);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка);
	
	НажатОК=	Ложь;
	
	Если Параметры.РежимИспользования = 1 Тогда 
		Заголовок=									"Отказ в подписи";
		Элементы.Комментарий.Заголовок=				"Комментарий по отказу";
		Элементы.ФормаОтказатьВПодписи.Заголовок= 	"Отказать в подписи";
		Элементы.ДекорацияПодписанта.Заголовок=		"Документ будет подписан";
	ИначеЕсли Параметры.РежимИспользования = 2 Тогда
		Заголовок=									"Согласование";
		Элементы.Комментарий.Заголовок=				"Комментарий к согласованию";
		Элементы.ФормаОтказатьВПодписи.Заголовок= 	"Согласовать";
		Элементы.ДекорацияПодписанта.Заголовок=		"Документ будет согласован";
	ИначеЕсли Параметры.РежимИспользования = 3 Тогда
		Заголовок=									"Отказ в согласовании";
		Элементы.Комментарий.Заголовок=				"Комментарий по отказу в согласовании";
		Элементы.ФормаОтказатьВПодписи.Заголовок= 	"Отказать в согласовании";
		Элементы.ДекорацияПодписанта.Заголовок=		"Документ будет отказан в согласовании";
	ИначеЕсли Параметры.РежимИспользования = 4 Тогда
		Заголовок=									"Уведомление об уточнении";
		Элементы.Комментарий.Заголовок=				"Комментарий к запросу";
		Элементы.ФормаОтказатьВПодписи.Заголовок= 	"Отправить уведомление";
		Элементы.ДекорацияПодписанта.Заголовок=		"Уведомление будет подписано";
	ИначеЕсли Параметры.РежимИспользования = 5 Тогда
		Заголовок=									"Запрос на аннулирование";
		Элементы.Комментарий.Заголовок=				"Комментарий к запросу";
		Элементы.ФормаОтказатьВПодписи.Заголовок= 	"Аннулировать";
		Элементы.ДекорацияПодписанта.Заголовок=		"Соглашение об аннулировании будет подписано";
	ИначеЕсли Параметры.РежимИспользования = 6 Тогда
		Заголовок=									"Отказ в аннулировании";
		Элементы.Комментарий.Заголовок=				"Комментарий к отказу";
		Элементы.ФормаОтказатьВПодписи.Заголовок= 	"Отказать";
		Элементы.ДекорацияПодписанта.Заголовок=		"Соглашение об аннулировании будет подписано";
	ИначеЕсли Параметры.РежимИспользования = 7 Тогда
		Заголовок=									"Уведомление об уточнении";
		Элементы.Комментарий.Заголовок=				"Комментарий к уведомлению";
		Элементы.ФормаОтказатьВПодписи.Заголовок= 	"Отправить уведомление";
		Элементы.ДекорацияПодписанта.Заголовок=		"Уведомление будет подписано";
	Иначе
		ВызватьИсключение("Не задан параметр контекста использования!");
	КонецЕсли;	

	Если Параметры.AuthenticateType = "Login" Тогда
		Элементы.ДекорацияПодписанта.Видимость= 	Ложь;
		Элементы.ДекорацияПодписанта.Доступность= 	Ложь;
		Элементы.ПредставлениеПодписи.Видимость= 	Ложь;
		Элементы.ПредставлениеПодписи.Доступность= 	Ложь;
	КонецЕсли;
	
	ПредставлениеДокумента=	Параметры.ПредставлениеДокумента;
	ПредставлениеПодписи=	Параметры.ПредставлениеПодписи;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПлатформаПриОткрытии(Отказ);

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ПлатформаПриЗакрытии();
	
КонецПроцедуры

